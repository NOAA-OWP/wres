import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption
import java.nio.file.StandardCopyOption
import java.time.ZoneId
// TODO: cannot install miniconda in current build environment
// Enable this when we have more control over the build environment
//import com.pswidersk.gradle.python.VenvTask

/* Used by gradle plugins outside the official gradle plugins repository */
buildscript
{
    repositories
    {
        mavenCentral()
    }
    dependencies
    {
        // For markdown -> html
        classpath 'com.atlassian.commonmark:commonmark:0.17.0'
        // To create a unified javadoc across submodules and top-level module:
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
    }
}

/* Gradle plugins available in the official gradle plugins repository */
plugins
{
    // Static analysis tool sonarqube client
    id 'org.sonarqube' version '3.1.1'

    //id 'com.github.jacobono.jaxb' version '1.3.5'
    // Above is unmaintained, less gradle-y, below fork has several improvements
    id 'org.openrepose.gradle.plugins.jaxb' version '2.5.0'

    // For ability to use git ids for versioning jar files:
    id 'org.ajoberstar.grgit' version '4.1.0'

    // To compile user documentation (not working: blows up on compileMarkdown)
    //id 'org.uulib.gradle.markdown' version '0.0.1'

    // To generate de/serialization classes for message bodies
    id 'com.google.protobuf' version '0.8.15'

    // To create trusted certificate files (.jks aka cacerts) from dir of .pem
    // (Not working: not friendly to our directory structure nor submodules)
    //id 'de.chkpnt.truststorebuilder' version '0.2.57'

    // To print junit test reports to the console in addition to html, so that
    // remote developers can see detailed test feedback without launching a
    // browser. See #61601
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'

    // To discover library versions with known vulnerabilities
    id 'org.owasp.dependencycheck' version '6.1.1'
    
    // Task to assist in downloading artifacts
    id "de.undercouch.download" version "4.1.1"

    // To discover library versions with updates available
    id 'com.github.ben-manes.versions' version '0.38.0'

    // Run python scripts
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //id 'com.pswidersk.python-plugin' version '1.2.1'
    
    // Markdown to html
    id 'org.kordamp.gradle.markdown' version '2.2.0'
}

repositories
{
    mavenCentral()
    maven 
    {
        url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases/"// 
    }
}


subprojects
{
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    // Each module does not need a version update unless code in it has changed.
    def gitCommit = grgit.log(paths: [project.name, 'build.gradle'], maxCommits:1).find()
    // Null check in case there is a new module with no history
    if( gitCommit != null )
    {
        def gitCommitId = gitCommit.id;
        def gitCommitDate = gitCommit.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
    }
    // Default version string for new module with no history
    else
    {
        String currentDate = OffsetDateTime.now( ZoneOffset.UTC ).format('yyyyMMdd' )
        version = currentDate + '-0000000'
    }

    // Whenever there are local changes, flag the version with a "-dev" suffix.
    // If "-dev" shows up in a version in production, either a bad artifact
    // made it into production or there is a bug in this logic. In either case,
    // the response is to make sure that a clean build producing a version with
    // no "-dev" occurs and to put that artifact into production.
    def gitIsClean = grgit.status().isClean()

    if (!gitIsClean)
    {
        version = version + '-dev'
    }

    sourceSets
    {
        main
        {
            java
            {
                // Standard sources in src. The dist being here is for eclipse
                // purposes. Eclipse needs the conf files to be labeled as
                // source or library or class in order to land on its runtime
                // or debugtime classpath.

                // It seems unintuitive but somehow works because there are no
                // actual java files inside the conf directory, this just
                // causes the files to show up on eclipse's classpath at
                // runtime or debugtime.
                // https://stackoverflow.com/questions/18183677/gradle-add-folder-to-eclipse-classpath#18184414
                srcDirs = ['src', 'dist/lib/conf']
            }

            // "resources" here end up included inside the jar.
            // For non-jarred resources such as config files, see distributions
            // section inside wres-core, as well as startScripts section.
            resources
            {
                srcDirs = ['nonsrc']
            }
        }

        test
        {
            java
            {
                srcDirs = ['test']
            }
            resources
            {
                srcDirs = ['testNonsrc']
            }
        }
    }

    repositories
    {
        mavenCentral()
        maven 
        {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases/"// 
            
        }
    }

    test
    {
        maxHeapSize = "1680m"
        testLogging.showStandardStreams = true
        systemProperties += [ 'user.timezone': 'UTC' ]
    }

    jar
    {
        // toward reproducible builds:
        preserveFileTimestamps = false
        reproducibleFileOrder = true

        // version number should be included
        manifest
        {
            attributes("Implementation-Title": "Water Resources Evaluation Service",
                       "Implementation-Version": version)
        }
    }

    java
    {
        withJavadocJar()
        withSourcesJar()
    }
}

project(':wres-util')
{
    dependencies
    {
        compile project(':wres-datamodel')

        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'

        compile 'com.google.guava:guava:30.1-jre'
        compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

        compile('edu.ucar:cdm-core:5.4.1')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }

        // Update the org.apache.httpcomponents modules depended upon by cdm
        compile 'org.apache.httpcomponents:httpclient:4.5.13'
        compile 'org.apache.httpcomponents:httpcore:4.4.14'
        compile 'org.apache.httpcomponents:httpmime:4.5.13'

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        // Because cdm uses apache commons logging, bridge to slf4j
        runtime 'org.slf4j:jcl-over-slf4j:2.0.0-alpha1'

        testCompile 'junit:junit:4.13.2'

        // Mocking help
        testCompile 'org.mockito:mockito-core:3.8.0'

        // super-duper mocking help (mocking static, private methods)
        testCompile 'org.powermock:powermock-api-mockito2:2.0.9'
        testCompile 'org.powermock:powermock-module-junit4:2.0.9'
    }
}

project(':wres-system')
{
    dependencies
    {
        compile project(':wres-util')
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'

        compile 'com.google.guava:guava:30.1-jre'

        // to use native postgres copy, need this on compile, otherwise runtime
        compile 'org.postgresql:postgresql:42.2.19'
        compile group: 'org.liquibase', name: 'liquibase-core', version: '4.3.1'

        // connection pooling:
        compile 'com.zaxxer:HikariCP:4.0.3'

        // Because org.postgresql:postgresql uses JUL logging, bridge it, here
        // at compile time because we need to expose a class to JUL.
        compile 'org.slf4j:jul-to-slf4j:2.0.0-alpha1'

        //JB @ 02/16/17
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: '30.1-jre'

        testCompile 'junit:junit:4.13.2'

        // Use mockserver to test certificate utilities
        testCompile 'org.mock-server:mockserver-netty:5.11.2'
    }

    test
    {
        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties += [ "mockserver.maxSocketTimeout": 120000 ]
    }
}

project(':wres-datamodel')
{
    dependencies
    {
        api project(':wres-config')
        api project(':wres-statistics')
        api 'org.apache.commons:commons-lang3:3.12.0'

        implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
        implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
        implementation 'com.github.ben-manes.caffeine:caffeine:3.0.0'

        // The following four provide units of measure. If used in public
        // method signatures, they will be upgraded to api (not implementation).
        api 'javax.measure:unit-api:2.1.2'
        implementation 'si.uom:si-units:2.0.1'
        implementation 'tech.units:indriya:2.1.2'
        implementation 'systems.uom:systems-common:2.0.2'

        // JCIP annotations
        compileOnly group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

        // Mocking help
        testImplementation 'org.mockito:mockito-core:3.8.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'

        // Backwards compatibility for JUnit 4 tests
        testCompileOnly 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.7.1'
    }
    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}

project(':wres-io')
{
    dependencies
    {
        compile project(':wres-util')
        compile project(':wres-system')
        compile project(':wres-datamodel')
        compile project(':wres-config')
        compile project(':wres-grid')
        compile project(':wres-events')

        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

        // JSON deserialization uses jackson directly. Specify recent version.
        compile 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.12.2'
        compile 'com.fasterxml.jackson.core:jackson-core:2.12.2'

        // For Java 8 java.time support
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.2'

        compile('edu.ucar:cdm-core:5.4.1')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }

        compile group: 'org.apache.commons', name: 'commons-compress', version: '1.20'

        // to use native postgres copy, need this on compile, otherwise runtime
        compile 'org.postgresql:postgresql:42.2.19'

        // connection pooling:
        compile 'com.zaxxer:HikariCP:4.0.3'

        // wres.io.utilities uses javax.json.* imports
        compile 'org.glassfish:javax.json:1.1.4'

        // Easier MD5
        compile 'commons-codec:commons-codec:1.15'

        // Better-than-Java-11's HTTP client
        compile 'com.squareup.okhttp3:okhttp:4.10.0-RC1'

        // Update vulnerable kotlin-stdlib that okhttp/okio uses
        compile 'org.jetbrains.kotlin:kotlin-stdlib:1.4.31'

        // Because org.postgresql:postgresql uses JUL logging, bridge at runtime
        runtime 'org.slf4j:jul-to-slf4j:2.0.0-alpha1'

        // Because cdm and aws sdk uses apache commons logging, bridge to slf4j
        runtime 'org.slf4j:jcl-over-slf4j:2.0.0-alpha1'

        //JB @ 02/16/17
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: '30.1-jre'

        testCompile group: 'org.liquibase', name: 'liquibase-core', version: '4.3.1'

        // Mocking help
        testCompile 'org.mockito:mockito-core:3.8.0'

        // TODO: remove dependence on powermock, remove powermock completely.
        testCompile 'org.powermock:powermock-api-mockito2:2.0.9'
        testCompile 'org.powermock:powermock-module-junit4:2.0.9'

        // Use mockserver as a mock server for mock API responses
        testCompile 'org.mock-server:mockserver-netty:5.11.2'

        // Use H2 for truly embedded SQL db
        testCompile 'com.h2database:h2:1.4.200'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'

        // Backwards compatibility for JUnit 4 tests
        testCompileOnly 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.7.1'

        // In-memory implementation of a java.nio.file abstract file system api
        // to test writing of files without actually writing files on the host machine
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.2'
    }

    test
    {
        useJUnitPlatform()

        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties += [ "mockserver.maxSocketTimeout": 120000,
                             "ucar.unidata.io.http.maxReadCacheSize": 200000,
                             "ucar.unidata.io.http.httpBufferSize": 200000 ]
    }


    // The liquibase changesets are in the root project(!?), give wres-io access
    // to those changesets on the test time classpath.
    sourceSets
    {
        test
        {
            resources
            {
                // Flatten the contents of both testNonsrc (that every
                // subproject aka module has) and root's dist/lib/conf
                // (including database dir) onto the classpath.
                // The reason to do this is that newer liquibase scripts can
                // start their existence at test-time, but both those new and
                // existing in production are accessible at test time.
                // The reason for awkward phrasing here is that we want the
                // classpath path to be exactly 'database/[changeset].xml', not
                // flattened out to '[changeset].xml'.
                srcDirs += '../dist/lib/conf'

                // Avoid inclusion of wresconfig.xml because the db settings
                // from that config would be (unsuccessfully) used at test time.
                excludes += 'wresconfig.xml'
            }
        }
    }
}

project(':wres-metrics')
{
    dependencies
    {
        compile project(':wres-datamodel')
        compile project(':wres-config')
        compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'

        testCompile project (':wres-datamodel')
        testCompile 'junit:junit:4.13.2'
        testCompile 'org.mockito:mockito-core:3.8.0'
    }
}

project(':wres-statistics')
{
    apply plugin: 'com.google.protobuf'

    // Python bindings and wheel from python script
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //apply plugin: 'com.pswidersk.python-plugin'

    // Html from markdown
    apply plugin: 'org.kordamp.gradle.markdown'
    
    def generatedDir = "${project.projectDir}/src"

    protobuf
    {
        // Automatically download and use the correct protobuf compiler
        protoc
        {
            artifact = 'com.google.protobuf:protoc:3.15.5'
        }
        // So all IDEs/editors can see generated files, use actual src dir:
        generatedFilesBaseDir = "${generatedDir}"
        
        // Generate python bindings: more long-winded as java is a default option
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    // Python sources
                    python { 
                        outputSubDir = 'python'
                    }
                }
            }
        }
    }
    
    sourceSets
    {
        main
        {
            // Deviate from our typical source tree here to work around plugin's
            // inflexibility on paths plus IDEs handling generated code, see
            // https://github.com/google/protobuf-gradle-plugin/issues/190
            // The different srcDirs seems to work for eclipse, not IDEA.
            // But somehow a bunch of weird src tree shows up too.
            java
            {
                srcDirs = ['src/main/java']
            }

            // Tell protobuf plugin where to find .proto schema files:
            proto
            {
                srcDirs = ['nonsrc']
            }
        }
    }
    
    javadoc
    {
        // Protobuf does not appear to generate javadoc, so neither will we.
        excludes = ['**/generated/*.java']
    }
    
    // Set the python version to use
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //pythonPlugin {
    //    pythonVersion = "3.8.2"
    //}
    
    // Generate a python wheel artifact for the wres protobuf bindings for use in python applications
    // Also generate an html document from markdown
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //task (buildProtobufBindingsForPython, type: VenvTask, dependsOn: ['generateProto'] ) {
    //    workingDir = file( "src/main/python" )
    //    args = ["wresproto.py"]
    //}
    
    task copyProtoSchemaToDist(type:Copy, dependsOn: markdownToHtml ) {
        description = 'Copies the Protobuf schema from nonsrc to dist/src.'
        destinationDir = file("${project.projectDir}/dist/wresproto/src/wresproto")
        delete fileTree(dir: destinationDir)
        from fileTree("${project.projectDir}/nonsrc/wresproto").include("*.proto")
        into destinationDir
    }
    
    // Generate html from markdown
    markdownToHtml
    {
       sourceDir = new File( "${project.projectDir}/dist/wresproto/doc" )
       outputDir = new File( "${project.projectDir}/dist/wresproto/doc" )
       // Enable all options
       markdownToHtml.all = true
    }
    
    // Clean the generated java bindings before regenerating them: #86722
    task cleanProtoGeneratedDir() {
        def javaBindingsDir = file("${project.projectDir}/src/main/java/wres")
        delete fileTree(dir: javaBindingsDir).include('**/generated/*.java')
    }
    
    // Tie the preparation of the Protobuf distribution into the java compile task for convenience. 
    compileJava.dependsOn copyProtoSchemaToDist

    // Tie the clean of the protobuf java bindings directory to the compile task
    compileJava.dependsOn cleanProtoGeneratedDir

    // Tie the python wheel generation into the java compile task for convenience
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //compileJava.dependsOn buildProtobufBindingsForPython

    dependencies
    {
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        compile 'com.google.protobuf:protobuf-java:3.15.5'
        compile project(':wres-config')

        // JMS API ( QPID client implements 1.1, but 1.1 is not available on maven )
        // https://issues.sonatype.org/browse/MVNCENTRAL-348
        testCompile group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        testCompile 'junit:junit:4.13.2'
        testCompile project( ':wres-eventsbroker' )
    }
}

// Return /etc/system-release file as a string if readable, else empty string.
String readElInfo()
{
    def elInfoPath = Paths.get( '/etc/system-release' );
    def elInfoReadable = Files.isReadable( elInfoPath );

    if ( elInfoReadable )
    {
        return Files.readString( elInfoPath );
    }

    return '';
}

project(':wres-vis')
{
    apply plugin: 'application'

    // Workaround windows paths that are too long by overriding the explicit setting of individual dependencies
    startScripts {
        classpath = files( '$APP_HOME/lib/*' )
        
        // Note: see lengthy note in main application below
        classpath += files( 'conf' )
    }

    mainClassName = 'wres.vis.client.GraphicsClient'
    applicationDefaultJvmArgs = ['-Xms512m','-Xmx512m',
                                 '-XX:+HeapDumpOnOutOfMemoryError']

    // Make available logging configuration to the classpath
    distributions
    {
        main
        {
            contents
            {
                from
                {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }

    dependencies
    {
        compile project(':wres-datamodel')
        compile project(':wres-util')
        compile project(':wres-eventsbroker')
        compile project(':wres-events')

        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
        compile group: 'com.sun.xml.fastinfoset', name: 'FastInfoset', version: '2.0.0'
        compile 'com.google.guava:guava:30.1-jre'
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        // For ohdcommonchps.jar:
        runtime group: 'org.slf4j', name: 'log4j-over-slf4j', version: '2.0.0-alpha1'

        compile files('../libs/external/jcommon-1.0.12.jar')
        compile files('../libs/external/jfreechart-1.0.9.jar')
        compile files('../libs/external/ohdcommonchps.jar')
        
        // SVG graphics from JFreeChart instances
        compile group: 'org.jfree', name: 'jfreesvg', version: '3.4'
        
        testCompile 'junit:junit:4.13.2'

        // Mocking help
        testCompile 'org.mockito:mockito-core:3.8.0'

        testRuntime files('dist/lib/conf')
    }


    // Check if the dependency modules version is later. If so, update this
    // module to that later version.
    def gitCommitOfDependencies = grgit.log(paths: ['wres-datamodel',
                                                    'wres-util',
                                                    'wres-eventsbroker',
                                                    'wres-events'], maxCommits:1).find()

    // Null check in case there is a new module with no history
    if( gitCommitOfDependencies != null )
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id;
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        def gitCommitOfThisModule = grgit.log( paths: [project.name, 'build.gradle'], maxCommits:1 ).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        def versionOfDependencies = gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if ( gitCommitDateOfDependencies.isAfter( gitCommitDateOfThisModule ) )
        {
            version = versionOfDependencies

            if ( !grgit.status().isClean() )
            {
                version = version + '-dev'
            }
        }
    }
}

project(':wres-config')
{
    apply plugin: 'org.openrepose.gradle.plugins.jaxb'

    dependencies
    {
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'

        // For Java 9+ compatibility:
        compile 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'

        compile group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics-runtime', version: '0.12.0'

        // To know about @Location
        compile 'org.glassfish.jaxb:jaxb-core:2.3.0.1'

        runtime 'org.glassfish.jaxb:jaxb-runtime:2.3.3'

        runtime 'org.eclipse.persistence:org.eclipse.persistence.moxy:2.7.8'

        testCompile 'junit:junit:4.13.2'

        // May help in future with #37380, may need to modify sources
        // available at https://git.apache.org/cxf-xjc-utils.git in dv dir:
        // xjc 'org.apache.cxf.xjcplugins:cxf-xjc-dv:3.2.0'
        jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.12.0'
        jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'
        jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.1.0'

        // commons-beanutils is transitive dependency of jaxb2-basics, has CVEs
        jaxb 'commons-beanutils:commons-beanutils:1.9.4'

        // jaxb-xjc transitively includes bind-api and jaxb-core
        jaxb 'org.glassfish.jaxb:jaxb-xjc:2.3.3'
        jaxb 'com.github.sabomichal:immutable-xjc-plugin:1.6.2'
    }

    def generatedIntermediateDir = "${project.buildDir}/generated"

    jaxb
    {
        xsdDir = "${project.projectDir}/nonsrc"
        bindingsDir = xsdDir

        xjc
        {
            taskClassname = 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
            generatePackage = 'wres.config.generated'
            destinationDir = "${generatedIntermediateDir}"
            // For location information to be in classes, -Xlocator is needed
            // here. For location information to be in instances at runtime, the
            // eclipselink moxy library is required at runtime (see runtime dep)

            // JBr: added -Xequals and -XhashCode implementations. Also added 
            // -Xinject-code to allow for wres.config.generated.ProjectConfig 
            // to implement java.lang.Comparable. See discussion in #44996

            args = [ '-Xinject-code', '-Xinheritance', '-Xannotate',
                     '-immutable', '-Xlocator', '-Xequals', '-XhashCode',
                     '-XtoString' ]
            // May help in future with #37380:
            //args = ['-Xinheritance', '-Xannotate', '-immutable', '-Xlocator', '-Xdv']
            producesDir = "${xsdDir}"
        }
    }

    // Struggled to find a way to get great incremental builds, ended up using
    // an additional copy task. The idea is that xjc task will drop files into
    // the build directory as an intermediate directory. The whole build dir
    // gets cleaned up on "clean". Now, in copyGenerated we copy from "build"
    // into "src" but cleaning up the src before doing so, to avoid extra files
    // lying around whenever the xsd file changes. This task will happen every
    // time, unfortunately, however, the xsd generation and java compilation
    // still will have the desired incremental properties (and dependents such
    // as jar).
    // Incremental builds without this extra task could probably be fixed at
    // https://github.com/rackerlabs/gradle-jaxb-plugin
    //
    // Also, when the plugin fixes
    // https://github.com/rackerlabs/gradle-jaxb-plugin/issues/6 we should get
    // the latest version (this seems to happen on each build)
    task copyGenerated(type:Copy, dependsOn: xjc, group: 'Parse') {
        description = 'Ensures generated code is created and available in src.'
        destinationDir = file("${project.projectDir}/src")
        delete fileTree(dir: destinationDir).include('**/generated/*.java')
        from fileTree("${generatedIntermediateDir}")
        into destinationDir
    }

    // Wiring the generation of jaxb classes into standard compilation task:
    compileJava.dependsOn copyGenerated

    javadoc
    {
        // XJC generates Java8-unfriendly javadoc. Do not bother generating.
        excludes = ['**/generated/*.java']
    }
}

project(':wres-grid')
{
    dependencies
    {
        compile project(':wres-config')
        compile project(':wres-util')
        compile project(':wres-system')
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        compile('edu.ucar:cdm-core:5.4.1')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        // Because cdm uses apache commons logging, bridge to slf4j
        runtime 'org.slf4j:jcl-over-slf4j:2.0.0-alpha1'

        testCompile 'junit:junit:4.13.2'
    }
}

project(':wres-messages')
{
    apply plugin: 'com.google.protobuf'

    def generatedDir = "${project.projectDir}/src"

    protobuf
    {
        // Automatically download and use the correct protobuf compiler
        protoc
        {
            artifact = 'com.google.protobuf:protoc:3.15.5'
        }
        // So all IDEs/editors can see generated files, use actual src dir:
        generatedFilesBaseDir = "${generatedDir}"
    }

    sourceSets
    {
        main
        {
            // Deviate from our typical source tree here to work around plugin's
            // inflexibility on paths plus IDEs handling generated code, see
            // https://github.com/google/protobuf-gradle-plugin/issues/190
            // The different srcDirs seems to work for eclipse, not IDEA.
            // But somehow a bunch of weird src tree shows up too.
            java
            {
                srcDirs = ['src/main/java']
            }

            // Tell protobuf plugin where to find .proto schema files:
            proto
            {
                srcDirs = ['nonsrc']
            }
        }
    }

    javadoc
    {
        // Protobuf does not appear to generate javadoc, so neither will we.
        excludes = ['**/generated/*.java']
    }

    dependencies
    {
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        compile 'com.google.protobuf:protobuf-java:3.15.5'

        testCompile 'junit:junit:4.13.2'
    }
}


project(':wres-worker')
{
    dependencies
    {
        compile project( ':wres-messages' )
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        compile 'com.rabbitmq:amqp-client:5.11.0'
        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        testCompile 'junit:junit:4.13.2'
    }

    apply plugin: 'application'

    mainClassName = 'wres.worker.Worker'
    applicationDefaultJvmArgs = ['-Xms32m','-Xmx32m',
                                 '-XX:+HeapDumpOnOutOfMemoryError']

    // Add logging configuration to the classpath when running via script
    startScripts
    {
        // Note: see lengthy note in main application below
        classpath += files('conf')
    }

    // Make available logging configuration to the classpath
    distributions
    {
        main
        {
            contents
            {
                from
                {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }

    // Check if the dependency modules version is later. If so, update this
    // module to that later version.
    def gitCommitOfDependencies = grgit.log(paths: ['wres-messages'], maxCommits:1).find()

    // Null check in case there is a new module with no history
    if( gitCommitOfDependencies != null )
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id;
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        def gitCommitOfThisModule = grgit.log( paths: [project.name, 'build.gradle'], maxCommits:1 ).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        def versionOfDependencies = gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if ( gitCommitDateOfDependencies.isAfter( gitCommitDateOfThisModule ) )
        {
            version = versionOfDependencies

            if ( !grgit.status().isClean() )
            {
                version = version + '-dev'
            }
        }
    }
}

project(':wres-tasker')
{
    dependencies
    {
        compile project( ':wres-messages' )
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        compile 'com.rabbitmq:amqp-client:5.11.0'
        compile 'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0'
        compile 'jakarta.annotation:jakarta.annotation-api:2.0.0'

        // For Java 9+ compatibility:
        compile 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.0'

        // Server library to run a web server:
        compile 'org.eclipse.jetty:jetty-server:11.0.1'

        // Support HTTP/2
        compile 'org.eclipse.jetty.http2:http2-server:11.0.1'

        // Support ALPN
        compile 'org.eclipse.jetty:jetty-alpn-java-server:11.0.1'

        // Servlet container library to run a web application with:
        compile 'org.eclipse.jetty:jetty-webapp:11.0.1'

        // Needed at compile-time to reference ServletContainer.class and
        // DefaultServlet.class
        compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:3.0.1'

        // To temporarily cache/store-in-RAM job metadata when redis unavailable
        compile 'com.github.ben-manes.caffeine:caffeine:3.0.0'

        // To accurately detect mime types of files.
        compile 'org.apache.tika:tika-core:1.25'

        // To persist job metadata while keeping it relatively fast
        compile 'org.redisson:redisson:3.15.1'

        // For ToStringBuilder
        compile 'org.apache.commons:commons-lang3:3.12.0'

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'
        runtime 'org.glassfish.jersey.core:jersey-server:3.0.1'

        // Needed to prevent "java.lang.IllegalStateException: InjectionManagerFactory not found."
        // Thanks, https://luohuahuang.org/2017/11/13/jersey-illegalstateexception-injectionmanagerfactory/
        runtime 'org.glassfish.jersey.inject:jersey-hk2:3.0.1'

        testCompile 'junit:junit:4.13.2'

        // To generate fake certificates and keys for testing
        testCompile 'org.bouncycastle:bcpkix-jdk15on:1.68'
        testCompile 'org.bouncycastle:bcprov-jdk15on:1.68'

        // To test against an embedded AMQP broker
        testCompile 'org.apache.qpid:qpid-broker-core:8.0.4'
        // Provides support for both 0-8 and 0-9-1
        testCompile 'org.apache.qpid:qpid-broker-plugins-amqp-0-8-protocol:8.0.4'
        testCompile 'org.apache.qpid:qpid-broker-plugins-memory-store:8.0.4'
    }

    apply plugin: 'application'

    mainClassName = 'wres.tasker.Tasker'
    applicationDefaultJvmArgs = ['-Xms340m','-Xmx340m',
                                 '-XX:+HeapDumpOnOutOfMemoryError',
                                 '-Djdk.tls.ephemeralDHKeySize=3072',
                                 '-Djdk.tls.rejectClientInitiatedRenegotiation=true']

    // Add static html files to the classpath when running via script
    startScripts
    {
        // Note: see lengthy note in main application below
        classpath += files('static')
        classpath += files('conf')
    }

    // Make available static html files to classpath, also logging configuration
    distributions
    {
        main
        {
            contents
            {
                from
                {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }


    // Check if the dependency modules version is later. If so, update this
    // module to that later version.
    def gitCommitOfDependencies = grgit.log(paths: ['wres-messages'], maxCommits:1).find()

    // Null check in case there is a new module with no history
    if( gitCommitOfDependencies != null )
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id;
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        def gitCommitOfThisModule = grgit.log( paths: [project.name,'build.gradle'], maxCommits:1 ).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

        def versionOfDependencies = gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if ( gitCommitDateOfDependencies.isAfter( gitCommitDateOfThisModule ) )
        {
            version = versionOfDependencies

            if ( !grgit.status().isClean() )
            {
                version = version + '-dev'
            }
        }
    }
}

project(':wres-eventsbroker')
{
    dependencies
    {
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'
        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        compile 'org.apache.commons:commons-lang3:3.12.0'

        // JMS API ( QPID client implements 1.1, but 1.1 is not available on maven )
        // https://issues.sonatype.org/browse/MVNCENTRAL-348
        compile group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'
        
        // QPID broker
        compile 'org.apache.qpid:qpid-broker-core:8.0.4'

        // QPID broker support for AMQP 0.8 and 0.9.1 (not 1.0)
        compile 'org.apache.qpid:qpid-broker-plugins-amqp-0-8-protocol:8.0.4'

        // QPID embedded (aka in-memory) broker support
        compile 'org.apache.qpid:qpid-broker-plugins-memory-store:8.0.4'

        // https://mvnrepository.com/artifact/org.apache.qpid/qpid-broker-plugins-management-http
        // Adds too much bloat for the value at test time with an embedded broker
        //compile group: 'org.apache.qpid', name: 'qpid-broker-plugins-management-http', version: '8.0.4'

        // QPID JMS client for AMQP 0.X (not 1.0). JMS 1.1 support only
        compile group: 'org.apache.qpid', name: 'qpid-client', version: '6.4.0'

        testCompile 'junit:junit:4.13.2'
    }
}

project(':wres-events')
{
    dependencies
    {
        compile project( ':wres-eventsbroker' )
        compile project( ':wres-statistics' )
        compile 'org.slf4j:slf4j-api:2.0.0-alpha1'

        // JCIP annotations
        compile group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        // JMS API ( QPID client implements 1.1, but 1.1 is not available on maven )
        // https://issues.sonatype.org/browse/MVNCENTRAL-348
        compile group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'

        // Mocking help
        testCompile 'org.mockito:mockito-core:3.8.0'
        // Mocking final classes
        testImplementation 'org.mockito:mockito-inline:3.8.0'
    }

    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}


project( ':wres-external-services-tests' )
{
    // A separate zip allowing independent runs against external deps.
    // This project is similar to systests in that it is not intended
    // to run at the usual build or test time on the build server.
    // Instead, a separate zip is made available with the current
    // versions of the jars used to run integration tests that rely
    // on external services (aka external services availability
    // tests). At the usual build/test time, a zip is produced with
    // a gradle script inside it that can be used for testing later.
    apply plugin: 'distribution'

    dependencies
    {
        runtime project( ':wres-io' )
    }

    distributions
    {
        main
        {
            contents
            {
                // Test software and build script
                from( 'dist' )
                {
                    include( 'src/test/**/*.java' )
                    include( 'build.gradle' )
                    include( 'gradle.properties' )
                    include( 'settings.gradle' )
                }

                // Gradle software itself
                from('..')
                {
                    include 'gradlew'
                    include 'gradlew.bat'
                    include 'gradle/wrapper/*'
                }

                // Add the wres-io jar.
                from( project( ':wres-io' ).jar )
                {
                    into( 'lib' )
                }

                // Add all the stuff on which wres-io jar depends.
                from( project( ':wres-io').configurations.runtime.files )
                {
                    into( 'lib' )
                }

                // Add the DoD intermediate certificate to trust
                from( '../dist/lib/conf/dod_sw_ca-54_expires_2022-11.pem' )
                {
                    into( 'lib/conf' )
                }
            }
        }
    }
}

/* Single report for test coverage, thanks to
https://discuss.gradle.org/t/merge-jacoco-coverage-reports-for-multiproject-setups/12100/5
 */
apply plugin: 'jacoco'        // code coverage tool
apply plugin: 'org.sonarqube' // static analysis tool

task(jacocoRootReport, type: JacocoReport, group: 'Verification', dependsOn: [subprojects.test, test])
{
    description = 'Generates an all-subproject aggregate code coverage report.'

    additionalSourceDirs.setFrom files( subprojects.sourceSets.main.allSource.srcDirs )
    sourceDirectories.setFrom files( subprojects.sourceSets.main.allSource.srcDirs,
                                       sourceSets.main.allSource.srcDirs )
    classDirectories.setFrom files( subprojects.sourceSets.main.output,
                                      sourceSets.main.output )

    executionData.setFrom files( subprojects.findAll
                                 {
                                     it.jacocoTestReport.executionData.first().exists()
                                 }.jacocoTestReport.executionData,
                                 jacocoTestReport.executionData )

    doFirst
    {
        executionData.setFrom files(executionData.findAll { it.exists() })
    }
}


/**
 * Task to compile markdown to html.
 *
 * TODO: replace with plugin 'org.kordamp.gradle.markdown'
 */
task(buildHtmlFromMarkdown, group: 'Documentation')
{
    description = 'Compiles markdown documentation into html'
    def srcDir = "${project.projectDir}/documentation/src" // seems odd that we have to do this
    def destDir = "${project.buildDir}/doc" // define these and use them later.

    inputs.dir(srcDir)
    outputs.dir(destDir)

    doFirst {
        // in case a source was removed, get rid of all html files.
        delete fileTree(dir: destDir).include('**/*.html').include('**/*.css')
    }

    doLast {
        def destFile = new File(destDir)
        destFile.mkdirs()

        def parser = org.commonmark.parser.Parser.builder().build()
        def renderer = org.commonmark.renderer.html.HtmlRenderer.builder().build()
        inputs.files.each {
            def basePath = Paths.get( it.path )
                                .getFileName()
                                .toString()

            if ( it.path.endsWith( ".md" ) )
            {
                // yuck, but I haven't found the better way yet
                def srcIdx =  it.path.indexOf( srcDir )
                def baseFileName = basePath.substring( 0, basePath.length() - 3 )

                // The meat - using commonmark to transform md to html
                def document = parser.parseReader( new FileReader( it ) )
                def innerOutput = renderer.render( document )
                def header = "<!DOCTYPE html>\r\n"
                header += "<html><head>\r\n"
                header += "    <link rel='stylesheet' type='text/css' href='style.css' />\r\n"
                header += "</head>\r\n<body>\r\n"
                def footer = "</body>\r\n</html>\r\n"
                def output = header + innerOutput + footer
                def outputPath = Paths.get( destDir, baseFileName + '.html' )
                Files.write( outputPath,
                             output.getBytes(),
                             StandardOpenOption.CREATE )
            }
            else
            {
                def outputPath = Paths.get( destDir, basePath )
                Files.copy( it.toPath(),
                            outputPath,
                            StandardCopyOption.REPLACE_EXISTING )
            }
        }
    }
}

/**
 * Kind of like xjc, there is an intermediate location in the build directory
 * so that gradle clean will remove these files.
 */
task(copyDocs, type: Copy, group: 'Documentation', dependsOn: buildHtmlFromMarkdown)
{
    description = 'Copies documentation source to distribution directory.'

    destinationDir = file("${project.projectDir}/dist/doc")

    doFirst {
        // in case a source file was removed, clean up old html files.
        delete fileTree(dir: destinationDir).include('**/*.html')
    }

    from buildHtmlFromMarkdown
    into destinationDir
}

apply plugin: 'application'
apply plugin: 'java-library'

mainClassName = 'wres.Main'

sourceCompatibility = '11'
targetCompatibility = '11'

applicationDefaultJvmArgs = ['-Xms1680m', '-Xmx1680m',
                             '-XX:MaxDirectMemorySize=512m',
                             '-XX:+HeapDumpOnOutOfMemoryError',
                             '-Duser.timezone=UTC',
                             '-Djava.util.logging.config.class=wres.system.logging.JavaUtilLoggingRedirector',
                             '-Ducar.unidata.io.http.maxReadCacheSize=200000',
                             '-Ducar.unidata.io.http.httpBufferSize=200000' ]

// Get the commit and date from the TOP for this project, it is outermost.
// The top level commit will also reflect changes to this build.gradle script.
def gitCommit = grgit.head()
def gitCommitId = gitCommit.id
def gitCommitDate = gitCommit.getDate()
def gitIsClean = grgit.status().isClean()

version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
if (!gitIsClean)
{
    version = version + '-dev'
}

apply plugin: 'nebula-aggregate-javadocs'

apply plugin: 'org.owasp.dependencycheck'

dependencies
{
    compile project(':wres-util')
    compile project(':wres-system')
    compile project(':wres-datamodel')
    compile project(':wres-io')
    compile project(':wres-metrics')
    compile project(':wres-config')
    compile project(':wres-vis')
    compile project(':wres-events')
    compile project(':wres-eventsbroker')

    compile 'org.slf4j:slf4j-api:2.0.0-alpha1'

    compile 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'org.eclipse.jetty:jetty-server:9.4.38.v20210224'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.38.v20210224'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.33'
    // Update the jetty-continuation that above jersey dep depends on
    compile 'org.eclipse.jetty:jetty-continuation:9.4.38.v20210224'

    compile 'com.github.ben-manes.caffeine:caffeine:3.0.0'

    runtime 'org.glassfish.jersey.core:jersey-server:2.33'
    runtime 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.33'
    runtime 'org.glassfish.jersey.inject:jersey-hk2:2.33'

    runtime 'ch.qos.logback:logback-classic:1.3.0-alpha5'


    testCompile 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testCompile 'org.mockito:mockito-core:3.8.0'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

test
{
    useJUnitPlatform()
}

sourceSets
{
    main
    {
        java
        {
            srcDirs = ['src', 'dist/lib/conf']
        }

        resources
        {
            srcDirs = ['nonsrc']
        }
    }

    test
    {
        java
        {
            srcDirs = ['test']
            exclude 'wres/systests/**'
        }

        resources
        {
            srcDirs = ['testNonsrc']
        }
    }
}

// Allows this:
// gradlew run -PappArgs="['saveForecast','/path/to/file']"
run
{
    if (project.hasProperty("appArgs"))
    {
        args Eval.me(appArgs)
    }
    // Allow profiling during run
    //jvmArgs = ['-agentlib:hprof=cpu=samples,interval=5,depth=25']
}

// Default location in gradle is 'src/dist' but we want conf files that are
// outside the jar to be from the 'dist' directory.
distributions
{
    main
    {
        contents
        {
            from
            {
                // Note: to have files land in the distributed zip files,
                // must put inside lib directory inside below directory.
                // Must also add the new directory to classpath under
                // startScripts below. For each module that adds to dist,
                // add here for now until we can make it grab all of em.
                ['dist', 'wres-io/dist', 'wres-statistics/dist', 'LICENSE']
            }

            // vis will be split into its own application eventually.
            from( 'wres-vis/dist' )
            {
                exclude 'lib/conf/eventbroker.properties'
                exclude 'lib/conf/logback.xml'
            }

            // Surely there is a more elegant way to get all the source jars
            // from projects in the runtime or implementation configuration of
            // this project. Something less brittle than handles transitive deps
            // and so forth. Admitting defeat for now.
            from( project(':wres-util').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-system').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-datamodel').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-io').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-metrics').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-config').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-vis').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-events').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-eventsbroker').sourcesJar )
            {
                into 'src'
            }

            from( project(':wres-statistics').sourcesJar )
            {
                into 'src'
            }
        }
    }
}

// hook to get documentation into default zip/tar/etc.
processResources.dependsOn(copyDocs)

// Add configuration files to the classpath when running via script
startScripts
{
    // Note gradle in this context assumes lib directory precedes these
    // files. To add more outsidejar directories, they need to be
    // added to 'dist/lib' and then the directory name
    // can be added below.
    classpath += files('conf')
}

jar
{
    // toward reproducible builds:
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    // version number should be included
    manifest
    {
        attributes("Implementation-Title": "Water Resources Evaluation Service",
                   "Implementation-Version": version)
    }
}

java
{
    withJavadocJar()
    withSourcesJar()
}

defaultTasks 'installDist', 'test', 'javadoc', 'jacocorootreport'
