import org.commonmark.parser.Parser

import org.commonmark.renderer.html.HtmlRenderer

import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption
import java.nio.file.StandardCopyOption
import java.time.ZoneId

// TODO: Enable miniconda for python build tasks once supported in build
// environment
//import com.pswidersk.gradle.python.VenvTask

/* Used by gradle plugins outside the official gradle plugins repository */
buildscript {

    // Workaround for missing transitive dependency for plugin org.kordamp.gradle.markdown
    // https://github.com/kordamp/markdown-gradle-plugin/issues/36
    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module('com.overzealous:remark:1.1.0') using module('com.wavefront:remark:2023-07.07') because "not available on maven central anymore"
        }

        // Forcing a newer version than what comes in with default com.github.seanrl.jaxb
        // plugin below. This addresses a dependabot reported issue. See GitHub #478.
        resolutionStrategy.force('org.codehaus.groovy:groovy-all:3.0.25')
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        // For markdown -> html
        classpath 'com.atlassian.commonmark:commonmark:0.17.0'
        // To create a unified javadoc across submodules and top-level module:
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
    }
}

/* Gradle plugins available in the official gradle plugins repository */
plugins {
    // Static analysis tool sonarqube client
    id 'org.sonarqube' version '7.2.2.6593'

    //id 'com.github.jacobono.jaxb' version '1.3.5'
    // Above is unmaintained, less gradle-y, below fork has several improvements
    //id 'org.openrepose.gradle.plugins.jaxb' version '2.5.0'
    // Above has no releases since 2018, below fork supports gradle 7+
    // The groovy force in buildScript above relates to the below plugin. See GitHub #478.
    id 'com.github.seanrl.jaxb' version '3.0.2'

    // For ability to use git ids for versioning jar files:
    id 'org.ajoberstar.grgit' version '5.3.3'

    // To compile user documentation (not working: blows up on compileMarkdown)
    //id 'org.uulib.gradle.markdown' version '0.0.1'

    // To generate de/serialization classes for message bodies
    // Bumping this to 0.9.5 caused wres-statistics to fail
    id 'com.google.protobuf' version '0.9.1'

    // To create trusted certificate files (.jks aka cacerts) from dir of .pem
    // (Not working: not friendly to our directory structure nor submodules)
    //id 'de.chkpnt.truststorebuilder' version '0.2.57'

    // To print junit test reports to the console in addition to html, so that
    // remote developers can see detailed test feedback without launching a
    // browser. See #61601
    id 'com.adarshr.test-logger' version '4.0.0'

    // To discover library versions with known vulnerabilities
    id 'org.owasp.dependencycheck' version '12.1.9'

    // Task to assist in downloading artifacts
    id 'de.undercouch.download' version '5.6.0'

    // To discover library versions with updates available
    id 'com.github.ben-manes.versions' version '0.53.0'

    // Run python scripts
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //id 'com.pswidersk.python-plugin' version '1.2.1'

    // Markdown to html
    id 'org.kordamp.gradle.markdown' version '2.2.0'

    // Bumping from 2.2.26 causes several failures. Several are easy to resolve,
    // changing yaml to YAML and skip = 'true' to skip = true, but one is not:
    // https://github.com/swagger-api/swagger-core/issues/4813
    // To generate API documentation from JAX-RS annotations and javadocs
    id "io.swagger.core.v3.swagger-gradle-plugin" version "2.2.26"

    // To generate JPMS images more easily with 3rd-party "automatic" modules
    id 'org.beryx.jlink' version '3.2.0'

    // Integrated code coverage report
    id 'jacoco-report-aggregation'

    // Check for unused dependencies and transitive dependencies in the build file ./gradlew buildHealth
    id 'com.autonomousapps.dependency-analysis' version '2.19.0'
}

//Add CSV output to eh Jacoco report.
testCodeCoverageReport {
    reports {
        csv.required = true
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://artifacts.unidata.ucar.edu/repository/unidata-releases/"
    }
}

// The target Java version for use in a gradle toolchain
def final TARGET_JDK = 17

java {
    withJavadocJar()
    withSourcesJar()

    // Declare the Java version, self-contained to the build
    toolchain {
        languageVersion = JavaLanguageVersion.of(TARGET_JDK)
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.autonomousapps.dependency-analysis'

    // Each module does not need a version update unless code in it has changed.
    def gitCommit = grgit.log(paths: [project.name, 'build.gradle', 'gradle'], maxCommits: 1).find()

    // Default version string for new module with no history
    String currentDate = OffsetDateTime.now(ZoneOffset.UTC).format('yyyyMMdd')
    version = currentDate + '-0000000'

    // Null check in case there is a new module with no history
    if (gitCommit != null)
    {
        def gitCommitId = gitCommit.id
        def gitCommitDate = gitCommit.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
    }

    // Whenever there are local changes, flag the version with a "-dev" suffix.
    // If "-dev" shows up in a version in production, either a bad artifact
    // made it into production or there is a bug in this logic. In either case,
    // the response is to make sure that a clean build producing a version with
    // no "-dev" occurs and to put that artifact into production.
    def gitIsClean = grgit.status().isClean()

    if (!gitIsClean)
    {
        version = version + '-dev'
    }

    sourceSets {
        main {
            java {
                // Standard sources in src. The dist being here is for eclipse
                // purposes. Eclipse needs the conf files to be labeled as
                // source or library or class in order to land on its runtime
                // or debugtime classpath.

                // It seems unintuitive but somehow works because there are no
                // actual java files inside the conf directory, this just
                // causes the files to show up on eclipse's classpath at
                // runtime or debugtime.
                // https://stackoverflow.com/questions/18183677/gradle-add-folder-to-eclipse-classpath#18184414
                srcDirs = ['src', 'dist/lib/conf']
            }

            // "resources" here end up included inside the jar.
            // For non-jarred resources such as config files, see distributions
            // section inside wres-core, as well as startScripts section.
            resources {
                srcDirs = ['nonsrc']
            }
        }

        test {
            java {
                srcDirs = ['test']
            }
            resources {
                srcDirs = ['testNonsrc']
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "https://artifacts.unidata.ucar.edu/repository/unidata-releases/"
        }
    }

    test {
        maxHeapSize = "512m"
        testLogging.showStandardStreams = true
        systemProperties += ['user.timezone': 'UTC']
    }

    jar {
        // version number should be included
        manifest {
            attributes("Implementation-Title": "Water Resources Evaluation Service",
                    "Implementation-Version": archiveVersion)
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()

        // Declare the Java version, self-contained to the build
        toolchain {
            languageVersion = JavaLanguageVersion.of(TARGET_JDK)
        }
    }

    tasks.withType(AbstractArchiveTask) {
        archiveVersion = project.version

        // Make bit-for-bit reproducible jars, zips, and tars.
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    // Truly prevent these annoying jars from showing up in dist zip
    configurations.implementation {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }

    // Truly prevent these annoying jars from showing up in dist zip
    configurations.runtimeOnly {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }
}

project(':wres-system') {
    apply plugin: 'com.github.seanrl.jaxb'

    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.17'

        implementation 'org.apache.commons:commons-lang3:3.20.0'

        // to use native postgres copy, need this on compile, otherwise runtime
        // Bumping this past this version causes massive slowdowns, see this ticket for more information
        // https://github.com/NOAA-OWP/wres/issues/454
        runtimeOnly('org.postgresql:postgresql:42.7.8') {

            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
        }

        // For Java 9+ compatibility:
        api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.4'

        // JAXB runtime
        runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.6'

        // Builders
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
        annotationProcessor 'org.projectlombok:lombok:1.18.42'

        // Database drivers need to be on the runtime classpath in order to
        // acquire connections initially. See #103431
        runtimeOnly 'com.h2database:h2:2.4.240'

        // Java Flight Recorder statistics for JDBC
        runtimeOnly 'com.github.marschall:jfr-jdbc:0.5.0'

        // Fast-Infoset encoded XML
        implementation group: 'com.sun.xml.fastinfoset', name: 'FastInfoset', version: '2.1.1'

        // JCIP annotations
        compileOnly group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

        testImplementation 'junit:junit:4.13.2'

        // Use mockserver to test certificate utilities.
        // Various transitive dependencies need to be overridden to avoid
        // triggering alerts related to security vulnerabilities.
        // See GitHub #478. 
        testImplementation('org.mock-server:mockserver-netty:5.15.0') {
            exclude group: 'commons-io', module: 'commons-io'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.google.guava', module: 'guava'
            exclude group: 'com.jayway.jsonpath', module: 'json-path'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'io.netty', module: 'netty-codec-http'
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'io.netty', module: 'netty-common'
            exclude group: 'io.netty', module: 'netty-handler'
            exclude group: 'net.minidev', module: 'json-smart'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
            exclude group: 'org.xmlunit', module: 'xmlunit-core'
            exclude group: 'org.yaml', module: 'snakeyaml'
        }
        testRuntimeOnly 'com.google.guava:guava:33.5.0-jre'
        testRuntimeOnly 'io.netty:netty-codec-http:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-codec-http2:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-common:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-handler:4.2.9.Final'
        testRuntimeOnly 'org.bouncycastle:bcpkix-jdk18on:1.83'
        testRuntimeOnly 'org.bouncycastle:bcprov-jdk18on:1.83'
        testRuntimeOnly 'commons-beanutils:commons-beanutils:1.11.0'
    }

    test {
        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties += ["mockserver.maxSocketTimeout": 120000]
    }
}

project(':wres-datamodel') {
    dependencies {
        api project(':wres-config')
        api project(':wres-statistics')

        api 'org.apache.commons:commons-lang3:3.20.0'

        implementation 'org.slf4j:slf4j-api:2.0.17'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        api group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
        implementation('com.github.ben-manes.caffeine:caffeine:3.2.3') {
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
        }

        // The following five libraries together provide units of measure.
        api 'javax.measure:unit-api:2.2'
        runtimeOnly 'si.uom:si-units:2.2.3'
        implementation 'tech.units:indriya:2.2.3'
        implementation 'systems.uom:systems-ucum:2.2'
        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        implementation 'si.uom:si-quantity:2.2.3'
        implementation 'systems.uom:systems-quantity:2.2'

        // For geometries
        api group: 'org.locationtech.jts', name: 'jts-core', version: '1.20.0'

        // JCIP annotations
        compileOnly group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

        // Mocking help
        testImplementation 'org.mockito:mockito-core:5.21.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Backwards compatibility for JUnit 4 tests
        testCompileOnly 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'

        testRuntimeOnly 'com.google.protobuf:protobuf-java:3.25.6'
        testImplementation 'org.hamcrest:hamcrest-core:3.0'
        testRuntimeOnly 'si.uom:si-quantity:2.2.3'
        testImplementation 'si.uom:si-units:2.2.3'
    }
    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}

project(':wres-io') {
    dependencies {
        api project(':wres-system')
        api project(':wres-datamodel')
        api project(':wres-statistics')
        api project(':wres-config')
        api project(':wres-reading')

        // Java Flight Recorder statistics for JDBC
        implementation 'com.github.marschall:jfr-jdbc:0.5.0'

        implementation('edu.ucar:cdm-core:5.9.1') {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
            // CVE-2021-33813
            exclude group: 'org.jdom', module: 'jdom2'
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
            // Remove older guava version. TODO: remove when the CDM catches up
            exclude group: 'com.google.guava', module: 'guava'
        }
        // Include later dependency versions for the excluded dependencies above
        implementation 'com.google.guava:guava:33.5.0-jre'

        // To use native postgres copy, need this on compile, otherwise runtime
        // In case of migration slowdowns, see: https://github.com/NOAA-OWP/wres/issues/454
        implementation('org.postgresql:postgresql:42.7.8') {
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
        }

        // Use H2 for truly embedded SQL db
        runtimeOnly 'com.h2database:h2:2.4.240'

        // Connection pooling:
        implementation 'com.zaxxer:HikariCP:7.0.2'

        // To cache
        implementation('com.github.ben-manes.caffeine:caffeine:3.2.3') {
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
        }

        // For geometries
        implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.20.0'

        // Bumping this past this version causes massive slowdowns, see this ticket for more information
        // https://github.com/NOAA-OWP/wres/issues/454
        implementation 'org.liquibase:liquibase-core:4.31.1'

        implementation 'commons-codec:commons-codec:1.20.0'

        // Use instead of the bridge between JUL and SLF4J. #60801-283
        runtimeOnly 'com.mattbertolini:liquibase-slf4j:5.1.0'

        compileOnly 'net.jcip:jcip-annotations:1.0'

        // Because org.postgresql:postgresql uses JUL logging, bridge at runtime
        runtimeOnly 'org.slf4j:jul-to-slf4j:2.1.0-alpha1'

        // Because cdm and aws sdk uses apache commons logging, bridge to slf4j
        runtimeOnly 'org.slf4j:jcl-over-slf4j:2.1.0-alpha1'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Backwards compatibility for JUnit 4 tests
        testImplementation 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'

        // In-memory implementation of a java.nio.file abstract file system api
        // to test writing of files without actually writing files on the host machine
        testRuntimeOnly group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'

        api 'org.apache.commons:commons-lang3:3.20.0'
        api 'org.slf4j:slf4j-api:2.0.17'
        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        testImplementation 'org.mockito:mockito-core:5.21.0'
    }

    test {
        useJUnitPlatform()

        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties += ["mockserver.maxSocketTimeout"          : 120000,
                             "ucar.unidata.io.http.maxReadCacheSize": 200000,
                             "ucar.unidata.io.http.httpBufferSize"  : 200000]
    }


    // Give wres-io access to the liquibase changesets on the expected classpath
    // at test time.
    sourceSets {
        test {
            resources {
                // Flatten the contents of /dist/lib/conf so that the classpath
                // path to the liquibase changesets is exactly
                // 'database/[changeset].xml'.
                srcDirs += 'dist/lib/conf'
            }
        }
    }
}

project(':wres-writing') {
    // Check if the dependency modules version is later. If so, update this
    // module to that later version.
    // See #105526: there is an ordering issue whereby the correct
    // project.version does not propagate to the application plugin tasks
    // (distZip, distTar etc.) unless the project.version is determined and
    // set before the application plugin is applied
    def gitCommitOfDependencies = grgit.log(paths: ['wres-datamodel',
                                                    'wres-eventsbroker',
                                                    'wres-events'], maxCommits: 1).find()

    // Null check in case there is a new module with no history
    if (gitCommitOfDependencies != null)
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def gitCommitOfThisModule = grgit.log(paths: [project.name, 'build.gradle', 'gradle'], maxCommits: 1).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def versionOfDependencies =
                gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if (gitCommitDateOfDependencies.isAfter(gitCommitDateOfThisModule))
        {
            version = versionOfDependencies

            println("Set wres-writing version: " + project.version)

            if (!grgit.status().isClean())
            {
                version = version + '-dev'
            }
        }
    }

    apply plugin: 'application'

    // Workaround windows paths that are too long by overriding the explicit setting of individual dependencies
    startScripts {
        classpath = files('$APP_HOME/lib/*')

        // Note: see lengthy note in main application below
        classpath += files('conf')
    }

    mainClassName = 'wres.writing.client.WritingClient'
    applicationDefaultJvmArgs = ['-Xms640m', '-Xmx640m',
                                 '-XX:+HeapDumpOnOutOfMemoryError',
                                 '-XX:+CrashOnOutOfMemoryError',
                                 '-Djava.util.logging.config.file=logging.properties',
                                 '-Dorg.jboss.logging.provider=slf4j']

    distributions {
        main {
            println(project.version)
            contents {
                from {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }

    dependencies {
        implementation project(':wres-reading')
        implementation project(':wres-system')
        implementation project(':wres-datamodel')
        implementation project(':wres-statistics')
        implementation project(':wres-config')
        implementation project(':wres-events')
        implementation project(':wres-eventsbroker')

        implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

        implementation('edu.ucar:cdm-core:5.9.1') {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
            // CVE-2021-33813
            exclude group: 'org.jdom', module: 'jdom2'
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
            // Remove older guava version. TODO: remove when the CDM catches up
            exclude group: 'com.google.guava', module: 'guava'
        }
        // Include later dependency versions for the excluded dependencies above
        implementation 'com.google.guava:guava:33.5.0-jre'

        // To accurately detect mime types of files.
        runtimeOnly 'org.apache.tika:tika-core:3.2.3'

        // For geometries
        implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.20.0'

        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        implementation 'org.apache.commons:commons-lang3:3.20.0'
        implementation 'org.slf4j:slf4j-api:2.0.17'
        testRuntimeOnly 'com.google.protobuf:protobuf-java:3.25.6'
        testRuntimeOnly 'org.apache.commons:commons-lang3:3.20.0'
        testImplementation 'org.apache.tika:tika-core:3.2.3'
        testImplementation 'org.mockito:mockito-core:5.21.0'
        testRuntimeOnly 'org.slf4j:slf4j-api:2.0.17'
        runtimeOnly 'com.fasterxml.jackson.core:jackson-core:2.20.1'
        runtimeOnly 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
        implementation 'org.apache.commons:commons-lang3:3.20.0'
        implementation 'org.slf4j:slf4j-api:2.0.17'
        testRuntimeOnly 'com.fasterxml.jackson.core:jackson-core:2.20.1'
        testRuntimeOnly 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
        testImplementation 'org.mockito:mockito-core:5.21.0'

        compileOnly 'net.jcip:jcip-annotations:1.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Backwards compatibility for JUnit 4 tests
        testImplementation 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'

        // In-memory implementation of a java.nio.file abstract file system api
        // to test writing of files without actually writing files on the host machine
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'
    }

    test {
        useJUnitPlatform()
    }
}

project(':wres-reading') {
    dependencies {
        api project(':wres-system')
        api project(':wres-datamodel')
        api project(':wres-statistics')
        api project(':wres-config')
        api project(':wres-http')

        implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

        // For Java 8 java.time support
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1'

        api('edu.ucar:cdm-core:5.9.1') {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
            // CVE-2021-33813
            exclude group: 'org.jdom', module: 'jdom2'
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
            // Remove older guava version. TODO: remove when the CDM catches up
            exclude group: 'com.google.guava', module: 'guava'
        }
        // Include later dependency versions for the excluded dependencies above
        implementation 'com.google.guava:guava:33.5.0-jre'

        implementation 'org.apache.commons:commons-compress:1.28.0'

        // CDM-S3 dependency for reading NetCDF from S3/GCS
        runtimeOnly 'edu.ucar:cdm-s3:5.9.1'

        // Better-than-Java's HTTP client
        implementation 'com.squareup.okhttp3:okhttp:5.3.2'

        implementation 'org.apache.httpcomponents:httpclient:4.5.14'

        // To accurately detect mime types of files.
        api 'org.apache.tika:tika-core:3.2.3'

        // For geometries
        implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.20.0'

        // Fast-Infoset encoded XML
        implementation group: 'com.sun.xml.fastinfoset', name: 'FastInfoset', version: '2.1.1'

        implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'

        // Bumping this past this version causes massive slowdowns, see this ticket for more information
        // https://github.com/NOAA-OWP/wres/issues/454
        runtimeOnly 'org.liquibase:liquibase-core:4.31.1'

        implementation group: 'org.eclipse.collections', name: 'eclipse-collections', version: '13.0.0'

        api 'com.fasterxml.jackson.core:jackson-annotations:2.20'
        api 'com.fasterxml.jackson.core:jackson-core:2.20.1'
        api 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
        api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.4'
        api 'org.apache.commons:commons-lang3:3.20.0'
        implementation 'com.opencsv:opencsv:5.12.0'
        implementation 'org.eclipse.collections:eclipse-collections-api:13.0.0'
        implementation 'org.slf4j:slf4j-api:2.0.17'
        testImplementation 'org.mockito:mockito-core:5.21.0'

        // Use instead of the bridge between JUL and SLF4J. #60801-283
        runtimeOnly 'com.mattbertolini:liquibase-slf4j:5.1.0'

        compileOnly 'net.jcip:jcip-annotations:1.0'

        // Builders
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
        annotationProcessor 'org.projectlombok:lombok:1.18.42'

        // Use mockserver as a mock server for mock API responses
        // Various transitive dependencies need to be overridden to avoid
        // triggering alerts related to security vulnerabilities.
        // See GitHub #478.
        testImplementation('org.mock-server:mockserver-netty:5.15.0') {
            exclude group: 'commons-io', module: 'commons-io'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.google.guava', module: 'guava'
            exclude group: 'com.jayway.jsonpath', module: 'json-path'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'io.netty', module: 'netty-codec-http'
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'io.netty', module: 'netty-common'
            exclude group: 'io.netty', module: 'netty-handler'
            exclude group: 'net.minidev', module: 'json-smart'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
            exclude group: 'org.xmlunit', module: 'xmlunit-core'
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        testImplementation('org.mock-server:mockserver-client-java:5.15.0') {
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
        }

        testImplementation('org.mock-server:mockserver-core:5.15.0') {
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
        }

        testRuntimeOnly 'com.google.guava:guava:33.5.0-jre'
        testRuntimeOnly 'io.netty:netty-codec-http:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-codec-http2:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-common:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-handler:4.2.9.Final'
        testRuntimeOnly 'org.bouncycastle:bcpkix-jdk18on:1.83'
        testRuntimeOnly 'org.bouncycastle:bcprov-jdk18on:1.83'
        testRuntimeOnly 'commons-beanutils:commons-beanutils:1.11.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Backwards compatibility for JUnit 4 tests
        testImplementation 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'

        // In-memory implementation of a java.nio.file abstract file system api
        // to test writing of files without actually writing files on the host machine
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'
    }

    test {
        useJUnitPlatform()

        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties += ["mockserver.maxSocketTimeout"          : 120000,
                             "ucar.unidata.io.http.maxReadCacheSize": 200000,
                             "ucar.unidata.io.http.httpBufferSize"  : 200000]
    }
}

project(':wres-eventdetection') {
    dependencies {
        api project(':wres-config')
        api project(':wres-datamodel')
        implementation project(':wres-statistics')
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        implementation 'org.apache.commons:commons-lang3:3.20.0'
        testRuntimeOnly 'com.google.protobuf:protobuf-java:3.25.6'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}

project(':wres-metrics') {
    dependencies {
        api project(':wres-datamodel')
        api project(':wres-config')
        api project(':wres-statistics')

        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        implementation 'org.apache.commons:commons-lang3:3.20.0'
        implementation 'org.eclipse.collections:eclipse-collections-api:13.0.0'
        testRuntimeOnly 'com.google.protobuf:protobuf-java:3.25.6'
        testRuntimeOnly 'org.apache.commons:commons-lang3:3.20.0'

        implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
        implementation 'org.slf4j:slf4j-api:2.0.17'

        implementation group: 'org.eclipse.collections', name: 'eclipse-collections', version: '13.0.0'

        compileOnly 'net.jcip:jcip-annotations:1.0'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        testImplementation 'org.mockito:mockito-core:5.21.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Backwards compatibility for JUnit 4 tests
        testCompileOnly 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'
    }

    test {
        useJUnitPlatform()
    }
}

project(':wres-statistics') {
    apply plugin: 'com.google.protobuf'

    // Python bindings and wheel from python script
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //apply plugin: 'com.pswidersk.python-plugin'

    // Html from markdown
    apply plugin: 'org.kordamp.gradle.markdown'

    def generatedDir = "${project.projectDir}/src"

    // Workaround to set duplicate strategy for all copy tasks.
    // See: https://github.com/gradle/gradle/issues/17236
    tasks.withType(Copy).all {
        duplicatesStrategy 'exclude'
    }

    protobuf {
        // Automatically download and use the correct protobuf compiler
        protoc {
            artifact = 'com.google.protobuf:protoc:3.25.6'
        }
        // So all IDEs/editors can see generated files, use actual src dir:
        generatedFilesBaseDir = "${generatedDir}"

        // Generate python bindings: more long-winded as java is a default option
        generateProtoTasks {
            all().each {task ->
                task.builtins {
                    // Python sources
                    python {
                        outputSubDir = 'python'
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            // Tell protobuf plugin where to find .proto schema files:
            proto {
                srcDirs = ['nonsrc']
            }
        }
    }

    // Set the python version to use
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //pythonPlugin {
    //    pythonVersion = "3.8.2"
    //}

    // Generate a python wheel artifact for the wres protobuf bindings for use in python applications
    // Also generate an html document from markdown
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //task (buildProtobufBindingsForPython, type: VenvTask, dependsOn: ['generateProto'] ) {
    //    workingDir = file( "src/main/python" )
    //    args = ["wresproto.py"]
    //}

    task copyProtoSchemaToDist(type: Copy, dependsOn: markdownToHtml) {
        description = 'Copies the Protobuf schema from nonsrc to dist/src.'
        destinationDir = file("${project.projectDir}/dist/wresproto/src/wresproto")
        delete fileTree(dir: destinationDir)
        from fileTree("${project.projectDir}/nonsrc/wresproto").include("*.proto")
        into destinationDir
    }

    // Generate html from markdown
    markdownToHtml {
        sourceDir = new File("${project.projectDir}/dist/wresproto/doc")
        outputDir = new File("${project.projectDir}/dist/wresproto/doc")
        // Enable all options
        markdownToHtml.all = true
    }

    // Clean the generated java bindings before regenerating them: #86722
    task cleanProtoGeneratedDir() {
        def javaBindingsDir = file("${project.projectDir}/src/main/java/wres")
        delete fileTree(dir: javaBindingsDir).include('**/generated/*.java')
    }

    // Tie the preparation of the Protobuf distribution into the java compile task for convenience.
    compileJava.dependsOn copyProtoSchemaToDist

    // Tie the clean of the protobuf java bindings directory to the compile task
    compileJava.dependsOn cleanProtoGeneratedDir

    // Tie the python wheel generation into the java compile task for convenience
    // TODO: cannot install miniconda in current build environment
    // Enable this when we have more control over the build environment
    //compileJava.dependsOn buildProtobufBindingsForPython

    dependencies {
        api 'com.google.protobuf:protobuf-java:3.25.6'

        implementation 'org.slf4j:slf4j-api:2.0.17'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }

    // Make this a Java Platform Module System (Java 9 jigsaw) module
    java.modularity.inferModulePath.set(true)
}

// Return /etc/system-release file as a string if readable, else empty string.
static String readElInfo()
{
    def elInfoPath = Paths.get('/etc/system-release')
    def elInfoReadable = Files.isReadable(elInfoPath)

    if (elInfoReadable)
    {
        return Files.readString(elInfoPath)
    }

    return ''
}

project(':wres-vis') {
    // Check if the dependency modules version is later. If so, update this
    // module to that later version.
    // See #105526: there is an ordering issue whereby the correct
    // project.version does not propagate to the application plugin tasks
    // (distZip, distTar etc.) unless the project.version is determined and
    // set before the application plugin is applied
    def gitCommitOfDependencies = grgit.log(paths: ['wres-datamodel',
                                                    'wres-eventsbroker',
                                                    'wres-events'], maxCommits: 1).find()

    // Null check in case there is a new module with no history
    if (gitCommitOfDependencies != null)
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def gitCommitOfThisModule = grgit.log(paths: [project.name, 'build.gradle', 'gradle'], maxCommits: 1).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def versionOfDependencies =
                gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if (gitCommitDateOfDependencies.isAfter(gitCommitDateOfThisModule))
        {
            version = versionOfDependencies

            println("Set wres-vis version: " + project.version)

            if (!grgit.status().isClean())
            {
                version = version + '-dev'
            }
        }
    }

    apply plugin: 'application'

    // Workaround windows paths that are too long by overriding the explicit setting of individual dependencies
    startScripts {
        classpath = files('$APP_HOME/lib/*')

        // Note: see lengthy note in main application below
        classpath += files('conf')
    }

    mainClassName = 'wres.vis.client.GraphicsClient'
    applicationDefaultJvmArgs = ['-Xms640m', '-Xmx640m',
                                 '-XX:+HeapDumpOnOutOfMemoryError',
                                 '-XX:+CrashOnOutOfMemoryError',
                                 '-Djava.util.logging.config.file=logging.properties',
                                 '-Dorg.jboss.logging.provider=slf4j']

    distributions {
        main {
            println(project.version)
            contents {
                from {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }

    dependencies {
        implementation project(':wres-config')
        implementation project(':wres-datamodel')
        implementation project(':wres-eventsbroker')
        implementation project(':wres-events')

        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'

        implementation 'org.slf4j:slf4j-api:2.0.17'

        implementation group: 'org.jfree', name: 'jfreechart', version: '1.5.6'

        // SVG graphics from JFreeChart instances
        implementation group: 'org.jfree', name: 'org.jfree.svg', version: '4.1'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        // For ohdcommonchps.jar:
        runtimeOnly group: 'org.slf4j', name: 'log4j-over-slf4j', version: '2.1.0-alpha1'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Backwards compatibility for JUnit 4 tests
        testImplementation 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'

        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        implementation project(':wres-statistics')

        testRuntimeOnly files('dist/lib/conf')

        // In-memory implementation of a java.nio.file abstract file system api
        // to test writing of files without actually writing files on the host machine
        testRuntimeOnly group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'
    }

    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}

project(':wres-config') {

    dependencies {
        api project(':wres-statistics')
        implementation 'org.slf4j:slf4j-api:2.0.17'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        // For YAML (de)serialization.
        implementation 'com.fasterxml.jackson:jackson-bom:2.20.1'
        api 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
        api 'com.fasterxml.jackson.core:jackson-annotations:2.20'
        api 'com.fasterxml.jackson.core:jackson-core:2.20.1'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.1'
        api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'
        implementation('com.hubspot.jackson:jackson-datatype-protobuf:0.9.18') {
            // Exclude old modules to eliminate several CVEs
            // Remove older protobuf version and include later version below
            exclude group: 'com.google.protobuf', module: 'protobuf-java'
            exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
            // Remove older guava version
            exclude group: 'com.google.guava', module: 'guava'
        }
        // Include later dependency versions for the excluded dependencies above
        api 'com.google.protobuf:protobuf-java:3.25.6'

        // To validate WKT strings as geometries
        api group: 'org.locationtech.jts', name: 'jts-core', version: '1.20.0'

        // Protobuf utilities
        implementation 'com.google.protobuf:protobuf-java-util:3.25.6'

        api 'org.apache.commons:commons-lang3:3.20.0'

        // For reading CSV thresholds
        implementation group: 'com.opencsv', name: 'opencsv', version: '5.12.0'

        // YAML/JSON schema validation
        api 'com.networknt:json-schema-validator:1.5.9'

        // To auto-generate builders for Java records through annotation processing
        annotationProcessor 'io.soabase.record-builder:record-builder-processor:51'
        compileOnly 'io.soabase.record-builder:record-builder-core:51'

        // To accurately detect mime types of files.
        api 'org.apache.tika:tika-core:3.2.3'

        api 'org.yaml:snakeyaml:2.5'

        // Mocking help
        testImplementation 'org.mockito:mockito-core:5.21.0'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // In-memory implementation of a java.nio.file abstract file system api
        // to test writing of files without actually writing files on the host machine
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'
    }

    // Set the output directory for sources generated via annotation processing
    compileJava {
        File generatedSourceDir = file("${project.projectDir}/build/generated")
        options.generatedSourceOutputDirectory = generatedSourceDir
    }

    test {
        useJUnitPlatform()
    }
}

project(':wres-messages') {
    apply plugin: 'com.google.protobuf'

    def generatedDir = "${project.projectDir}/src"

    // Workaround to set duplicate strategy for all copy tasks.
    // See: https://github.com/gradle/gradle/issues/17236
    tasks.withType(Copy).all {
        duplicatesStrategy 'exclude'
    }

    protobuf {
        // Automatically download and use the correct protobuf compiler
        protoc {
            artifact = 'com.google.protobuf:protoc:3.25.6'
        }
        // So all IDEs/editors can see generated files, use actual src dir:
        generatedFilesBaseDir = "${generatedDir}"
    }

    sourceSets {
        main {
            // Tell protobuf plugin where to find .proto schema files:
            proto {
                srcDirs = ['nonsrc']
            }
        }
    }

    dependencies {
        api 'com.google.protobuf:protobuf-java:3.25.6'

        implementation 'org.slf4j:slf4j-api:2.0.17'

        testImplementation 'junit:junit:4.13.2'
    }

    // Make this a Java Platform Module System (Java 9 jigsaw) module
    java.modularity.inferModulePath.set(true)
}

project(':wres-worker') {

    // Check if the dependency modules version is later. If so, update this
    // module to that later version.
    def gitCommitOfDependencies = grgit.log(paths: ['wres-messages', 'wres-http'], maxCommits: 1).find()

    // Null check in case there is a new module with no history
    if (gitCommitOfDependencies != null)
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def gitCommitOfThisModule = grgit.log(paths: [project.name, 'build.gradle'], maxCommits: 1).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def versionOfDependencies =
                gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if (gitCommitDateOfDependencies.isAfter(gitCommitDateOfThisModule))
        {
            version = versionOfDependencies

            println("Set wres-worker version: " + project.version)

            if (!grgit.status().isClean())
            {
                version = version + '-dev'
            }
        }
    }

    dependencies {
        implementation project(':wres-http')
        implementation project(':wres-messages')
        implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
        implementation 'com.squareup.okhttp3:okhttp:5.3.2'

        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'com.rabbitmq:amqp-client:5.28.0'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        implementation 'com.google.protobuf:protobuf-java:3.25.6'
        testImplementation 'org.mockito:mockito-core:5.21.0'

        // Use mockserver as a mock server for mock API responses
        // Various transitive dependencies need to be overridden to avoid
        // triggering alerts related to security vulnerabilities.
        // See GitHub #478.
        testImplementation('org.mock-server:mockserver-netty:5.15.0') {
            exclude group: 'commons-io', module: 'commons-io'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.google.guava', module: 'guava'
            exclude group: 'com.jayway.jsonpath', module: 'json-path'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'io.netty', module: 'netty-codec-http'
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'io.netty', module: 'netty-common'
            exclude group: 'io.netty', module: 'netty-handler'
            exclude group: 'net.minidev', module: 'json-smart'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
            exclude group: 'org.xmlunit', module: 'xmlunit-core'
            exclude group: 'org.yaml', module: 'snakeyaml'
        }

        testImplementation('org.mock-server:mockserver-client-java:5.15.0') {
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
        }

        testImplementation('org.mock-server:mockserver-core:5.15.0') {
            exclude group: 'io.netty', module: 'netty-codec-http2'
            exclude group: 'commons-beanutils', module: 'commons-beanutils'
            exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
            exclude group: 'org.bouncycastle', module: 'bcpkix-jdk18on'
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
        }

        testRuntimeOnly 'com.google.guava:guava:33.5.0-jre'
        testRuntimeOnly 'io.netty:netty-codec-http:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-codec-http2:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-common:4.2.9.Final'
        testRuntimeOnly 'io.netty:netty-handler:4.2.9.Final'
        testRuntimeOnly 'org.bouncycastle:bcpkix-jdk18on:1.83'
        testRuntimeOnly 'org.bouncycastle:bcprov-jdk18on:1.83'
        testRuntimeOnly 'commons-beanutils:commons-beanutils:1.11.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()

        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties += ["mockserver.maxSocketTimeout"          : 120000,
                             "ucar.unidata.io.http.maxReadCacheSize": 200000,
                             "ucar.unidata.io.http.httpBufferSize"  : 200000]
    }

    apply plugin: 'application'

    // Experiment with jlink version of the application. See #62732. This is in
    // addition to JPMS modules and is a possibility opened by use of modules.
    apply plugin: 'org.beryx.jlink'

    mainClassName = 'wres.worker.Worker'

    applicationDefaultJvmArgs = ['-Xms64m', '-Xmx64m',
                                 '-XX:+HeapDumpOnOutOfMemoryError']

    // Add logging configuration to the classpath when running via script
    startScripts {
        // Note: see lengthy note in main application below
        classpath += files('conf')
    }

    // Make available logging configuration to the classpath
    distributions {
        main {
            contents {
                from {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }

    // Make this a Java Platform Module System (Java 9 jigsaw) module
    java.modularity.inferModulePath.set(true)

    // Jlink is separate from making the jar a module and is at the application
    // level, but made possible by the use of JPMS modules.
    jlink {
        def myCustomMergedModuleName = 'wres.worker.mergedDepsModule'

        // The merged module is a technique from the org.beryx.jlink plugin that
        // aggregates/merges improper/automatic modules into a proper JPMS
        // module.
        mergedModuleName = myCustomMergedModuleName

        mergedModule {
            requires 'java.base'
            requires 'org.slf4j'
        }

        customImage {
            jdkModules = ['java.base', 'java.xml', 'java.desktop']
            appModules = ['wres.worker', myCustomMergedModuleName,
                          'ch.qos.logback.classic']
            options = ['--strip-debug', '--compress', '2', '--no-header-files',
                       '--no-man-pages']
        }

        launcher {
            jvmArgs = ['-Dlogback.configurationFile=./logback.xml']
        }
    }


    // Copy the logback.xml, other resources into bin. Perhaps we are supposed
    // to include these in the module, but this follows the pattern established
    // above and to-date where we keep these resources outside of jars.
    tasks.jlink.doLast {
        copy {
            from 'dist/lib/conf'
            into "$imageDir.asFile/bin"
        }
    }
}

project(':wres-tasker') {

    // Check if the dependency modules version is later. If so, update this
    // module to that later version. See #105526 for why this was moved up.
    //Order matters.
    def gitCommitOfDependencies = grgit.log(paths: ['wres-messages',
                                                    'wres-config'], maxCommits: 1).find()

    // Null check in case there is a new module with no history
    if (gitCommitOfDependencies != null)
    {
        def gitCommitIdOfDependencies = gitCommitOfDependencies.id
        def gitCommitDateOfDependencies = gitCommitOfDependencies.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def gitCommitOfThisModule = grgit.log(paths: [project.name, 'build.gradle', 'gradle'], maxCommits: 1).find()
        def gitCommitDateOfThisModule = gitCommitOfThisModule.dateTime.withZoneSameInstant(ZoneId.of("UTC"))

        def versionOfDependencies =
                gitCommitDateOfDependencies.format('yyyyMMdd') + '-' + gitCommitIdOfDependencies[0..6]

        if (gitCommitDateOfDependencies.isAfter(gitCommitDateOfThisModule))
        {
            version = versionOfDependencies

            println("Set wres-tasker version: " + project.version)

            if (!grgit.status().isClean())
            {
                version = version + '-dev'
            }
        }
    }

    configurations {
        // This separate configuration keeps swagger jars out of the zip.
        docGen
    }

    def swaggerDocsBaseDir = new File(buildDir, "swagger")
    def swaggerDocsSubdirPathString = 'lib/static/swagger/doc'

    dependencies {
        // To allow for programmatic post of input data:
        implementation project(':wres-config')
        implementation project(':wres-messages')
        implementation project(':wres-statistics')
        implementation project(':wres-http')
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'com.rabbitmq:amqp-client:5.28.0'
        implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'

        // Easy byte[] to hex conversion
        implementation 'commons-codec:commons-codec:1.20.0'

        //Jakarta servlet
        implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'

        // Server library to run a web server:
        implementation 'org.eclipse.jetty:jetty-server:12.1.5'

        // Servlet library is now tied to ee10.
        implementation 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.1.5'
        implementation 'org.eclipse.jetty.ee10:jetty-ee10-servlets:12.1.5'

        // Support HTTP/2
        implementation 'org.eclipse.jetty.http2:jetty-http2-server:12.1.5'

        // Support ALPN
        runtimeOnly 'org.eclipse.jetty:jetty-alpn-java-server:12.1.5'

        // Servlet container library to run a web application with:
        runtimeOnly 'org.eclipse.jetty.ee10:jetty-ee10-webapp:12.1.5'

        // Needed at compile-time to reference ServletContainer.class and
        // DefaultServlet.class
        implementation 'org.glassfish.jersey.containers:jersey-container-servlet-core:3.1.11'

        // To handle multipart post
        implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.1.11'

        // To temporarily cache/store-in-RAM job metadata when redis unavailable
        implementation('com.github.ben-manes.caffeine:caffeine:3.2.3') {
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
        }

        // To accurately detect mime types of files.
        implementation 'org.apache.tika:tika-core:3.2.3'

        // To persist job metadata while keeping it relatively fast
        implementation('org.redisson:redisson:3.51.0') {
            // Because of CVE-2022-24823. TODO: remove when Redisson catches up
            exclude group: 'io.netty'
        }

        // Because of CVE-2022-24823. TODO: remove when Redisson catches up
        implementation group: 'io.netty', name: 'netty-all', version: '4.2.9.Final'

        // Better-than-Java's HTTP client. Used to talk to manager.
        implementation 'com.squareup.okhttp3:okhttp:5.3.2'

        api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.4'

        implementation 'org.eclipse.jetty:jetty-alpn-server:12.1.5'
        implementation 'org.eclipse.jetty:jetty-http:12.1.5'
        implementation 'org.eclipse.jetty:jetty-util:12.1.5'
        implementation 'org.glassfish.jersey.core:jersey-common:3.1.11'

        // JSON deserialization uses jackson directly. Specify recent version.
        implementation 'com.fasterxml.jackson:jackson-bom:2.20.1'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
        runtimeOnly 'com.fasterxml.jackson.core:jackson-core:2.20.1'

        // For ToStringBuilder
        implementation 'org.apache.commons:commons-lang3:3.20.0'

        // To send/receive messages
        implementation 'com.google.protobuf:protobuf-java:3.25.6'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        runtimeOnly 'org.glassfish.jersey.core:jersey-server:3.1.11'

        // Needed to prevent "java.lang.IllegalStateException: InjectionManagerFactory not found."
        // Thanks, https://luohuahuang.org/2017/11/13/jersey-illegalstateexception-injectionmanagerfactory/
        runtimeOnly 'org.glassfish.jersey.inject:jersey-hk2:3.1.11'

        testImplementation 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // To generate fake certificates and keys for testing
        testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.83'
        testImplementation 'org.bouncycastle:bcprov-jdk18on:1.83'

        // To test against an embedded AMQP broker
        testImplementation 'org.apache.qpid:qpid-broker-core:10.0.1'
        // Provides support for both 0-8 and 0-9-1
        testRuntimeOnly 'org.apache.qpid:qpid-broker-plugins-amqp-0-8-protocol:10.0.1'
        testRuntimeOnly 'org.apache.qpid:qpid-broker-plugins-memory-store:10.0.1'
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'

        // To generate swagger's openapi.json at build time.
        docGen 'io.swagger.core.v3:swagger-jaxrs2-jakarta:2.2.41'
    }

    apply plugin: 'application'
    apply plugin: 'io.swagger.core.v3.swagger-gradle-plugin'

    mainClassName = 'wres.tasker.Tasker'
    applicationDefaultJvmArgs = ['-Xms340m', '-Xmx340m',
                                 '-XX:+HeapDumpOnOutOfMemoryError',
                                 '-Djdk.tls.ephemeralDHKeySize=3072',
                                 '-Djdk.tls.rejectClientInitiatedRenegotiation=true']

    // Add static html files to the classpath when running via script
    startScripts {
        // Note: see lengthy note in main application below
        classpath += files('static')
        classpath += files('conf')
    }

    // Make available openapi docs and logging configuration to the classpath
    distributions {
        main {
            contents {
                from {
                    // Source from the static content in dist, plus the swaggerDocs creation task
                    ['dist', swaggerDocs]
                }
                // Because this is appended directly to the API docs
                exclude '**/wres_api_info.yaml'
            }
        }
    }

    // Task named "resolve" is from from swagger plugin, generates openapi json
    resolve {
        def skipDocs = 'true'
        def sourceFile = file('dist/lib/static/swagger/wres_api_info.yaml')
        outputDir = new File(swaggerDocsBaseDir, swaggerDocsSubdirPathString)
        if (!skipDocs)
        {
            println("Merging the API documentation with the contents of: " + sourceFile)
            println("Writing the API documentation to: " + outputDir)
        }
        outputFileName = 'wres_openapi'
        outputFormat = 'yaml'
        prettyPrint = 'true'
        openApiFile = sourceFile
        classpath = sourceSets.main.runtimeClasspath
        resourcePackages = ['wres.tasker']
        buildClasspath = classpath
        buildClasspath += configurations.docGen

        // Disable the api documentation task until the swagger gradle plugin respects
        // the toolchain declaration. As of v2.2.8, it falls back on JAVA_HOME.
        // TODO: enable the documentation when the toolchain is respected
        skip = skipDocs
    }

    // Swagger docs copy task, which depends on the swagger docs creation task, aka "resolve"
    task(swaggerDocs, dependsOn: resolve, type: Copy) {
        from(fileTree(swaggerDocsBaseDir))
        into new File(buildDir, "swaggerDist")
    }
    test {
        useJUnitPlatform()
    }
}

project(':wres-eventsbroker') {
    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.17'

        // Apache ActiveMQ Artemis server
        implementation('org.apache.activemq:artemis-server:2.44.0') {
            // TODO: remove when Artemis catches up

            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'

            // Because we use slf4j, not jboss-logmanager:
            exclude group: 'org.jboss.logmanager', module: 'jboss-logmanager'

            // Because of CVE-2023-2976, CVE-2020-8908.
            exclude group: 'com.google.guava', module: 'guava'

            // Because of CVE-2024-29131, CVE-2024-29133.
            exclude group: 'org.apache.commons', module: 'commons-configuration2'

            // Because of CVE-2024-34447, CVE-2024-29857, CVE-2024-30171, CVE-2024-30172.
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'

            // Because of CVE-2024-47535.
            exclude group: 'io.netty', module: 'io.netty'
        }

        implementation group: 'io.netty', name: 'netty-all', version: '4.2.9.Final'

        // Include later dependency version
        runtimeOnly group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.83'

        // AMQP 1.0 protocol support for the ActiveMQ Artemis server
        runtimeOnly('org.apache.activemq:artemis-amqp-protocol:2.44.0') {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'

            // Because we use slf4j, not jboss-logmanager:
            exclude group: 'org.jboss.logmanager', module: 'jboss-logmanager'
        }

        // JCIP annotations
        compileOnly group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        // Because artemis uses apache commons logging, bridge to slf4j
        runtimeOnly 'org.slf4j:jcl-over-slf4j:2.1.0-alpha1'

        // Qpid AMQP 1.0 Jakarta Messaging API client to test interaction with the embedded broker
        testRuntimeOnly('org.apache.qpid:qpid-jms-client:2.9.0') {
            // Because of CVE-2022-24823. TODO: remove when Qpid catches up
            exclude group: 'io.netty'
        }

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }

}

project(':wres-events') {
    dependencies {
        api project(':wres-statistics')
        implementation 'org.slf4j:slf4j-api:2.0.17'

        // JCIP annotations
        api group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

        // Cache
        implementation('com.github.ben-manes.caffeine:caffeine:3.2.3') {
            // Not used at runtime, bloat
            exclude group: 'org.checkerframework', module: 'checker-qual'
        }

        // Jakarta Messaging API
        api group: 'jakarta.jms', name: 'jakarta.jms-api', version: '3.1.0'

        // Qpid AMQP 1.0 Jakarta Messaging API client
        runtimeOnly('org.apache.qpid:qpid-jms-client:2.9.0') {
            // Because of CVE-2022-24823. TODO: remove when Qpid catches up
            exclude group: 'io.netty'
        }

        // For various utilities
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'

        runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
            // Not used at runtime, bloat
            exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        }

        implementation 'com.google.protobuf:protobuf-java:3.25.6'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Mocking help
        testImplementation 'org.mockito:mockito-core:5.21.0'

        // To create an embedded broker for unit testing
        testImplementation project(':wres-eventsbroker')
    }

    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}

project(':wres-http') {
    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.17'

        // Better-than-Java's HTTP client
        api 'com.squareup.okhttp3:okhttp:5.3.2'

        implementation 'org.apache.commons:commons-lang3:3.20.0'

        // JUnit 5 API and runtime
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testImplementation 'org.mockito:mockito-core:5.21.0'
    }

    // Facilitate unit tests with JUnit
    test {
        useJUnitPlatform()
    }
}

// TODO: move this project into its own build.gradle.
project(':wres-external-services-tests') {
    // A separate zip allowing independent runs against external deps.
    // This project is similar to systests in that it is not intended
    // to run at the usual build or test time on the build server.
    // Instead, a separate zip is made available with the current
    // versions of the jars used to run integration tests that rely
    // on external services (aka external services availability
    // tests). At the usual build/test time, a zip is produced with
    // a gradle script inside it that can be used for testing later.
    apply plugin: 'distribution'

    dependencies {
        runtimeOnly project(':wres-reading')
    }

    // Explicitly link the artifact creation for this project to the
    // artifact creation for the projects on which it depends. This is
    // an anti-pattern as described here:
    // https://docs.gradle.org/current/samples/sample_cross_project_output_sharing.html
    // However, the suggested approach did not work in this case and
    // the dependencies must be clarified for gradle 8.0 and above:
    // https://docs.gradle.org/7.6/userguide/validation_problems.html#implicit_dependency
    // In any case, would be better to move this project into a separate
    // build.gradle, as noted above.
    distZip.dependsOn project(':wres-config').tasks.jar
    distZip.dependsOn project(':wres-datamodel').tasks.jar
    distZip.dependsOn project(':wres-statistics').tasks.jar
    distZip.dependsOn project(':wres-system').tasks.jar
    distTar.dependsOn project(':wres-http').tasks.jar
    distTar.dependsOn project(':wres-config').tasks.jar
    distTar.dependsOn project(':wres-datamodel').tasks.jar
    distTar.dependsOn project(':wres-statistics').tasks.jar
    distTar.dependsOn project(':wres-system').tasks.jar
    distTar.dependsOn project(':wres-http').tasks.jar

    distributions {
        main {
            contents {
                // Test software and build script
                from('dist') {
                    include('src/test/**/*.java')
                    include('build.gradle')
                    include('gradle.properties')
                    include('settings.gradle')
                }

                // Gradle software itself
                from('..') {
                    include 'gradlew'
                    include 'gradlew.bat'
                    include 'gradle/wrapper/*'
                }

                // Add the wres-reading jar.
                from(project(':wres-reading').jar) {
                    into('lib')
                }

                // Add all the stuff on which wres-reading jar depends.
                from(project(':wres-reading').configurations.runtimeClasspath.files) {
                    into('lib')
                }
            }
        }
    }
}

/**
 * Task to compile markdown to html.
 *
 * TODO: replace with plugin 'org.kordamp.gradle.markdown'*/
task(buildHtmlFromMarkdown, group: 'Documentation') {
    description = 'Compiles markdown documentation into html'
    def srcDir = "${project.projectDir}/documentation/src"
    // seems odd that we have to do this
    def destDir = "${project.buildDir}/doc"
    // define these and use them later.

    inputs.dir(srcDir)
    outputs.dir(destDir)

    doFirst {
        // in case a source was removed, get rid of all html files.
        delete fileTree(dir: destDir).include('**/*.html').include('**/*.css')
    }

    doLast {
        def destFile = new File(destDir)
        destFile.mkdirs()

        def parser = Parser.builder().build()
        def renderer = HtmlRenderer.builder().build()
        inputs.files.each {
            def basePath = Paths.get(it.path)
                    .getFileName()
                    .toString()

            if (it.path.endsWith(".md"))
            {
                // yuck, but I haven't found the better way yet
                def srcIdx = it.path.indexOf(srcDir)
                def baseFileName = basePath.substring(0, basePath.length() - 3)

                // The meat - using commonmark to transform md to html
                def document = parser.parseReader(new FileReader(it))
                def innerOutput = renderer.render(document)
                def header = "<!DOCTYPE html>\r\n"
                header += "<html><head>\r\n"
                header += "    <link rel='stylesheet' type='text/css' href='style.css' />\r\n"
                header += "</head>\r\n<body>\r\n"
                def footer = "</body>\r\n</html>\r\n"
                def output = header + innerOutput + footer
                def outputPath = Paths.get(destDir, baseFileName + '.html')
                Files.write(outputPath,
                        output.getBytes(),
                        StandardOpenOption.CREATE)
            }
            else
            {
                def outputPath = Paths.get(destDir, basePath)
                Files.copy(it.toPath(),
                        outputPath,
                        StandardCopyOption.REPLACE_EXISTING)
            }
        }
    }
}

/**
 * Kind of like xjc, there is an intermediate location in the build directory
 * so that gradle clean will remove these files.*/
task(copyDocs, type: Copy, group: 'Documentation', dependsOn: buildHtmlFromMarkdown) {
    description = 'Copies documentation source to distribution directory.'

    destinationDir = file("${project.projectDir}/dist/doc")

    doFirst {
        // in case a source file was removed, clean up old html files.
        delete fileTree(dir: destinationDir).include('**/*.html')
    }

    from buildHtmlFromMarkdown
    into destinationDir
}

apply plugin: 'jacoco' // code coverage tool
apply plugin: 'org.sonarqube' // static analysis tool
apply plugin: 'application'
apply plugin: 'java-library'
apply plugin: 'nebula-aggregate-javadocs'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'

mainClassName = 'wres.Main'

applicationDefaultJvmArgs = ['-Xms2560m', '-Xmx2560m',
                             '-XX:MaxDirectMemorySize=512m',
                             '-XX:+HeapDumpOnOutOfMemoryError',
                             '-XX:+CrashOnOutOfMemoryError',
                             '-Duser.timezone=UTC',
                             '-Djava.util.logging.config.class=wres.system.logging.JavaUtilLoggingRedirector',
                             '-Ducar.unidata.io.http.maxReadCacheSize=200000',
                             '-Ducar.unidata.io.http.httpBufferSize=200000',
                             '-Djava.util.logging.config.file=logging.properties',
                             '-Dorg.jboss.logging.provider=slf4j']

// See #95586. Use the ZGC garbage collector for a recent JDK, else CMS, else default.
if (TARGET_JDK >= 17)
{
    applicationDefaultJvmArgs += '-XX:+UseZGC'
}
else if (TARGET_JDK < 14)
{
    applicationDefaultJvmArgs += '-XX:+UseConcMarkSweepGC'
}
// Otherwise resort to default

// Get the commit and date from the TOP for this project, it is outermost.
// The top level commit will also reflect changes to this build.gradle script.
def gitCommit = grgit.head()
def gitCommitId = gitCommit.id
def gitCommitDate = gitCommit.dateTime.withZoneSameInstant(ZoneId.of("UTC"))
def gitIsClean = grgit.status().isClean()

version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
if (!gitIsClean)
{
    version = version + '-dev'
}

dependencies {
    implementation project(':wres-system')
    implementation project(':wres-datamodel')
    implementation project(':wres-io')
    implementation project(':wres-writing')
    implementation project(':wres-reading')
    implementation project(':wres-metrics')
    implementation project(':wres-config')
    implementation project(':wres-vis')
    implementation project(':wres-events')
    implementation project(':wres-eventsbroker')
    implementation project(':wres-messages')
    implementation project(':wres-eventdetection')

    implementation 'org.slf4j:slf4j-api:2.0.17'

    implementation 'com.google.protobuf:protobuf-java:3.25.6'
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'javax.measure:unit-api:2.2'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.eclipse.jetty:jetty-alpn-server:12.1.5'
    implementation 'org.eclipse.jetty:jetty-http:12.1.5'
    implementation 'org.eclipse.jetty:jetty-util:12.1.5'
    implementation project(':wres-statistics')

    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'

    //Jakarta servlet 
    implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'

    //Jetty servlet stuff now tied to ee10, with the move to Jetty 12.
    implementation 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.1.5'

    // Servlet container library to run a web application with:
    runtimeOnly 'org.eclipse.jetty:jetty-webapp:11.0.25'

    // Needed at compile-time to reference ServletContainer.class and
    // DefaultServlet.class
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet-core:3.1.11'

    // Jetty server
    implementation 'org.eclipse.jetty:jetty-server:12.1.5'

    implementation('com.github.ben-manes.caffeine:caffeine:3.2.3') {
        // Not used at runtime, bloat
        exclude group: 'org.checkerframework', module: 'checker-qual'
    }

    // To persist job metadata while keeping it relatively fast
    implementation('org.redisson:redisson:3.51.0') {
        // Because of CVE-2022-24823. TODO: remove when Redisson catches up
        exclude group: 'io.netty'
    }

    implementation('edu.ucar:cdm-core:5.9.1') {
        // Because we use slf4j, not jcl:
        exclude group: 'commons-logging', module: 'commons-logging'
        // CVE-2021-33813
        exclude group: 'org.jdom', module: 'jdom2'
        // Not used at runtime, bloat
        exclude group: 'org.checkerframework', module: 'checker-qual'
        //Remove older guava version. TODO: remove when the CDM catches up
        exclude group: 'com.google.guava', module: 'guava'
    }

    // JCIP annotations
    compileOnly group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

    runtimeOnly 'org.glassfish.jersey.containers:jersey-container-servlet:3.1.11'
    runtimeOnly 'org.glassfish.jersey.core:jersey-server:3.1.11'
    // currently locked at this version, check this ticket for more information
    // https://vlab.***REMOVED***/redmine/issues/124200
    runtimeOnly 'org.glassfish.jersey.containers:jersey-container-jetty-http:3.1.11'
    runtimeOnly 'org.glassfish.jersey.inject:jersey-hk2:3.1.11'

    // Support HTTP/2
    //implementation 'org.eclipse.jetty.http2:http2-server:11.0.24'
    implementation 'org.eclipse.jetty.http2:jetty-http2-server:12.1.5'

    // Support ALPN
    runtimeOnly 'org.eclipse.jetty:jetty-alpn-java-server:12.1.5'

    runtimeOnly('ch.qos.logback:logback-classic:1.5.23') {
        // Not used at runtime, bloat
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }

    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    // To auto-generate builders for Java records through annotation processing
    annotationProcessor 'io.soabase.record-builder:record-builder-processor:51'
    compileOnly 'io.soabase.record-builder:record-builder-core:51'

    // JUnit 5 API and runtime
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Backwards compatibility for JUnit 4 tests
    testCompileOnly 'junit:junit:4.13.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.14.1'

    testImplementation 'org.mockito:mockito-core:5.21.0'

}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs = ['src', 'dist/lib/conf']
        }

        resources {
            srcDirs = ['nonsrc']
        }
    }

    test {
        java {
            srcDirs = ['test']
            exclude 'wres/systests/**'
        }

        resources {
            srcDirs = ['testNonsrc']
        }
    }
}

// Allows this:
// gradlew run -PappArgs="['saveForecast','/path/to/file']"
run {
    if (project.hasProperty("appArgs"))
    {
        args Eval.me(appArgs)
    }
    // Allow profiling during run
    //jvmArgs = ['-agentlib:hprof=cpu=samples,interval=5,depth=25']
}

// Default location in gradle is 'src/dist' but we want conf files that are
// outside the jar to be from the 'dist' directory.
distributions {
    main {
        contents {
            from {
                // Note: to have files land in the distributed zip files,
                // must put inside lib directory. Must also add the lib
                // directory to classpath under startScripts below. For each
                // module that adds to dist, add here for now until we can make
                // it grab all of em.
                ['dist', 'wres-io/dist', 'wres-statistics/dist', 'LICENSE']
            }

            // vis will be split into its own application eventually.
            from('wres-vis/dist') {
                exclude 'lib/conf/eventbroker.properties'
                exclude 'lib/conf/logback.xml'
                exclude 'lib/conf/logging.properties'
                exclude 'lib/conf/LiberationSans-Regular.ttf'
            }

            // Surely there is a more elegant way to get all the source jars
            // from projects in the runtime or implementation configuration of
            // this project. Something less brittle that handles transitive deps
            // and so forth. Admitting defeat for now.
            from(project(':').sourcesJar) {
                into 'src'
            }

            from(project(':wres-system').sourcesJar) {
                into 'src'
            }

            from(project(':wres-datamodel').sourcesJar) {
                into 'src'
            }

            from(project(':wres-io').sourcesJar) {
                into 'src'
            }

            from(project(':wres-writing').sourcesJar) {
                into 'src'
            }

            from(project(':wres-reading').sourcesJar) {
                into 'src'
            }

            from(project(':wres-http').sourcesJar) {
                into 'src'
            }

            from(project(':wres-metrics').sourcesJar) {
                into 'src'
            }

            from(project(':wres-config').sourcesJar) {
                into 'src'
            }

            from(project(':wres-vis').sourcesJar) {
                into 'src'
            }

            from(project(':wres-events').sourcesJar) {
                into 'src'
            }

            from(project(':wres-eventsbroker').sourcesJar) {
                into 'src'
            }

            from(project(':wres-statistics').sourcesJar) {
                into 'src'
            }

            from(project(':wres-eventdetection').sourcesJar) {
                into 'src'
            }
        }
    }
}

// hook to get documentation into default zip/tar/etc.
processResources.dependsOn(copyDocs)

// Add configuration files to the classpath when running via script
startScripts {
    classpath = files('$APP_HOME/lib/*')

    // Note gradle in this context assumes lib directory precedes these
    // files. To add more outsidejar directories, they need to be
    // added to 'dist/lib' and then the directory name
    // can be added below.
    classpath += files('conf')
}

jar {
    // version number should be included
    manifest {
        attributes("Implementation-Title": "Water Resources Evaluation Service",
                "Implementation-Version": archiveVersion)
    }
}

tasks.withType(AbstractArchiveTask) {
    archiveVersion = project.version

    // Make bit-for-bit reproducible jars, zips, and tars.
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// Truly prevent these annoying jars from showing up in dist zip
configurations.implementation {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'org.checkerframework', module: 'checker-qual'
    exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.jboss.logmanager', module: 'jboss-logmanager'
}

// Truly prevent these annoying jars from showing up in dist zip
configurations.runtimeOnly {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'org.checkerframework', module: 'checker-qual'
    exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.jboss.logmanager', module: 'jboss-logmanager'
}

defaultTasks 'installDist', 'test', 'javadoc'
