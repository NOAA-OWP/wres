import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption
import java.nio.file.StandardCopyOption

/* used by sonarqube static analysis and git versioning */
buildscript
{
    repositories
    {
        mavenCentral()
    }
    dependencies
    {
        // for static analysis tool sonarqube:
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5'
        // For markdown -> html
        classpath 'com.atlassian.commonmark:commonmark:0.9.0'
        // To create a unified javadoc across submodules and top-level module:
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
    }
}

plugins
{
    //id 'com.github.jacobono.jaxb' version '1.3.5'
    // above is unmaintained, less gradle-y, below fork has several improvements
    id 'org.openrepose.gradle.plugins.jaxb' version '2.4.1'

    // for ability to use git ids for versioning jar files:
    id 'org.ajoberstar.grgit' version '1.7.2'

    // To compile user documentation (not working: blows up on compileMarkdown)
    //id 'org.uulib.gradle.markdown' version '0.0.1'

    // To generate de/serialization classes for message bodies
    id 'com.google.protobuf' version '0.8.5'

    // To create trusted certificate files (.jks aka cacerts) from dir of .pem
    // (Not working: not friendly to our directory structure nor submodules)
    //id 'de.chkpnt.truststorebuilder' version '0.2.57'

}

repositories
{
    mavenCentral()
    maven 
    {
        url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases/"// 
    }
}


subprojects
{
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    // Each module does not need a version update unless code in it has changed.
    def gitCommit = grgit.log(paths: [project.name], maxCommits:1).find()
    def gitCommitId = gitCommit.id
    def gitCommitDate = gitCommit.getDate()

    def gitIsClean = grgit.status().isClean()

    version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
    if (!gitIsClean)
    {
        version = version + '-dev'
    }

    sourceSets
    {
        main
        {
            java
            {
                // Standard sources in src. The dist being here is for eclipse
                // purposes. Eclipse needs the conf files to be labeled as
                // source or library or class in order to land on its runtime
                // or debugtime classpath.

                // It seems unintuitive but somehow works because there are no
                // actual java files inside the conf directory, this just
                // causes the files to show up on eclipse's classpath at
                // runtime or debugtime.
                // https://stackoverflow.com/questions/18183677/gradle-add-folder-to-eclipse-classpath#18184414
                srcDirs = ['src', 'dist/lib/conf']
            }

            // "resources" here end up included inside the jar.
            // For non-jarred resources such as config files, see distributions
            // section inside wres-core, as well as startScripts section.
            resources
            {
                srcDirs = ['nonsrc']
            }
        }

        test
        {
            java
            {
                srcDirs = ['test']
            }
            resources
            {
                srcDirs = ['testNonsrc']
            }
        }
    }

    repositories
    {
        mavenCentral()
        maven 
        {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases/"// 
            
        }
    }

    test
    {
        maxHeapSize = "3072m"
        testLogging.showStandardStreams = true

    }

    jar
    {
        // toward reproducible builds:
        preserveFileTimestamps = false
        reproducibleFileOrder = true

        // version number should be included
        manifest
        {
            attributes("Implementation-Title": "Water Resources Evaluation Service",
                       "Implementation-Version": version)
        }
    }
}

project(':wres-util')
{
    dependencies
    {
        compile project(':wres-datamodel')

        compile 'org.slf4j:slf4j-api:1.8.0-beta2'

        compile 'com.google.guava:guava:22.0'
        compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

        compile('edu.ucar:cdm:5.0.0-alpha3')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha4'

        // Because cdm uses apache commons logging, bridge to slf4j
        runtime 'org.slf4j:jcl-over-slf4j:1.8.0-beta2'

        testCompile 'junit:junit:4.12'

        // Mocking help
        testCompile 'org.mockito:mockito-core:2.8.47'

        // super-duper mocking help (mocking static, private methods)
        testCompile 'org.powermock:powermock-api-mockito2:1.7.1'
        testCompile 'org.powermock:powermock-module-junit4:1.7.1'

        testCompile files('../libs/external/EVS.jar')
    }
}

project(':wres-system')
{
    dependencies
    {
        compile project(':wres-util')
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'

        compile 'com.google.guava:guava:22.0'

        // to use native postgres copy, need this on compile, otherwise runtime
        compile 'org.postgresql:postgresql:42.2.1'
        compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.3'

        // connection pooling:
        compile 'com.mchange:c3p0:0.9.5.2'

        // Because org.postgresql:postgresql uses JUL logging, bridge it, here
        // at compile time because we need to expose a class to JUL.
        compile 'org.slf4j:jul-to-slf4j:1.8.0-beta2'

        //JB @ 02/16/17
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: '21.0'

        testCompile 'junit:junit:4.12'

        // Mocking help
        testCompile 'org.mockito:mockito-core:2.8.47'

        // super-duper mocking help (mocking static, private methods)
        testCompile 'org.powermock:powermock-api-mockito2:1.7.1'
        testCompile 'org.powermock:powermock-module-junit4:1.7.1'
    }
}

project(':wres-datamodel')
{
    dependencies
    {
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile 'org.apache.commons:commons-lang3:3.6'
        compile project(':wres-config')

        testCompile 'junit:junit:4.12'

        testCompile files('../libs/external/EVS.jar')
    }
}

project(':wres-io')
{
    dependencies
    {
        compile project(':wres-util')
        compile project(':wres-system')
        compile project(':wres-datamodel')
        compile project(':wres-config')
        compile project(':wres-vis')
        compile project(':wres-grid')

        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
        compile 'org.glassfish.jersey.core:jersey-common:2.27'
        compile 'org.glassfish.jersey.core:jersey-client:2.27'
        compile 'org.glassfish.jersey.inject:jersey-hk2:2.27'
        compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.27'

        compile('edu.ucar:cdm:5.0.0-alpha3')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }


        compile group: 'org.apache.commons', name: 'commons-compress', version: '1.14'

        compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.3'

        compile( group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.370')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }

        // to use native postgres copy, need this on compile, otherwise runtime
        compile 'org.postgresql:postgresql:42.2.1'

        // connection pooling:
        compile 'com.mchange:c3p0:0.9.5.2'

        // Because org.postgresql:postgresql uses JUL logging, bridge at runtime
        runtime 'org.slf4j:jul-to-slf4j:1.8.0-beta2'

        // Because cdm and aws sdk uses apache commons logging, bridge to slf4j
        runtime 'org.slf4j:jcl-over-slf4j:1.8.0-beta2'

        //JB @ 02/16/17
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: '21.0'

        testCompile 'junit:junit:4.12'

        // Mocking help
        testCompile 'org.mockito:mockito-core:2.8.47'

        // super-duper mocking help (mocking static, private methods)
        testCompile 'org.powermock:powermock-api-mockito2:1.7.1'
        testCompile 'org.powermock:powermock-module-junit4:1.7.1'

        // try out an embedded postgres
        testCompile 'ru.yandex.qatools.embed:postgresql-embedded:2.9'

        // Use mockserver as a mock server for mock API responses
        testCompile 'org.mock-server:mockserver-netty:5.4.1'
    }

    test
    {
        // Set the socket timeout for tests involving mockserver to 120s
        systemProperties = [ "mockserver.maxSocketTimeout": 120000 ]
    }
}

project(':wres-metrics')
{
    dependencies
    {
        compile project(':wres-datamodel')
        compile project(':wres-config')
        compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'

        testCompile project (':wres-datamodel')
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.8.47'
    }
}

project(':wres-vis')
{
    dependencies
    {
        compile project(':wres-datamodel')
        compile project(':wres-config')
        compile project(':wres-util')        

        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
        compile group: 'com.sun.xml.fastinfoset', name: 'FastInfoset', version: '1.2.6'
        compile 'com.google.guava:guava:22.0'
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha4'

        // For ohdcommonchps.jar:
        runtime group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.8.0-beta2'

        compile files('../libs/external/jcommon-1.0.12.jar')
        compile files('../libs/external/jfreechart-1.0.9.jar')
        compile files('../libs/external/ohdcommonchps.jar')

        testCompile project (':wres-datamodel')
        testCompile files('../libs/external/ohdcommonchps.jar')
        testCompile 'junit:junit:4.12'
        //Must load jfreechart explicitly before EVS.jar to avoid conflicting versions
        testCompile files('../libs/external/jfreechart-1.0.9.jar')
        testCompile files('../libs/external/EVS.jar')
        testRuntime files('dist/lib/conf')

    }

    configurations
    {
        testCompile
        {
            // to prevent the testCompile and testRuntime from inheriting this:
            exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
        }
    }
}

project(':wres-config')
{
    // apply plugin: 'com.github.jacobono.jaxb'
    apply plugin: 'org.openrepose.gradle.plugins.jaxb'

    dependencies
    {
        // For Java 9+ compatibility:
        compile 'javax.xml.bind:jaxb-api:2.2.11'

        // TODO: check that this is consistent with the declaration of dependencies elsewhere
        // and update if not. Added in the context of #44996
        // https://mvnrepository.com/artifact/org.jvnet.jaxb2_commons/jaxb2-basics-runtime
        compile group: 'org.jvnet.jaxb2_commons', name: 'jaxb2-basics-runtime', version: '1.11.1'

        runtime 'com.sun.xml.bind:jaxb-core:2.2.11'
        runtime 'com.sun.xml.bind:jaxb-impl:2.2.11'
    }

    dependencies
    {
        xjc 'com.github.sabomichal:immutable-xjc-plugin:1.3.3'
        // May help in future with #37380, may need to modify sources
        // available at https://git.apache.org/cxf-xjc-utils.git in dv dir:
        // xjc 'org.apache.cxf.xjcplugins:cxf-xjc-dv:3.2.0'
        jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:1.11.1'
        jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1'
        jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.0.2'
        jaxb 'com.sun.xml.bind:jaxb-core:2.2.11'
        jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.11'
    }

    def generatedIntermediateDir = "${project.buildDir}/generated"

    jaxb
    {
        xsdDir = "${project.projectDir}/nonsrc"
        bindingsDir = xsdDir

        xjc
        {
            taskClassname = 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
            generatePackage = 'wres.config.generated'
            destinationDir = "${generatedIntermediateDir}"
            // For location information to be in classes, -Xlocator is needed
            // here. For location information to be in instances at runtime, the
            // eclipselink moxy library is required at runtime (see runtime dep)

            // JBr: added -Xequals and -XhashCode implementations. Also added 
            // -Xinject-code to allow for wres.config.generated.ProjectConfig 
            // to implement java.lang.Comparable. See discussion in #44996

            args = ['-Xinject-code', '-Xinheritance', '-Xannotate', '-immutable', '-Xlocator', '-Xequals', '-XhashCode' ]
            // May help in future with #37380:
            //args = ['-Xinheritance', '-Xannotate', '-immutable', '-Xlocator', '-Xdv']
            producesDir = "${xsdDir}"
        }
    }

    // Struggled to find a way to get great incremental builds, ended up using
    // an additional copy task. The idea is that xjc task will drop files into
    // the build directory as an intermediate directory. The whole build dir
    // gets cleaned up on "clean". Now, in copyGenerated we copy from "build"
    // into "src" but cleaning up the src before doing so, to avoid extra files
    // lying around whenever the xsd file changes. This task will happen every
    // time, unfortunately, however, the xsd generation and java compilation
    // still will have the desired incremental properties (and dependents such
    // as jar).
    // Incremental builds without this extra task could probably be fixed at
    // https://github.com/rackerlabs/gradle-jaxb-plugin
    //
    // Also, when the plugin fixes
    // https://github.com/rackerlabs/gradle-jaxb-plugin/issues/6 we should get
    // the latest version (this seems to happen on each build)
    task copyGenerated(type:Copy, dependsOn: xjc, group: 'Parse') {
        description = 'Ensures generated code is created and available in src.'
        destinationDir = file("${project.projectDir}/src")
        delete fileTree(dir: destinationDir).include('**/generated/*.java')
        from fileTree("${generatedIntermediateDir}")
        into destinationDir
    }

    dependencies
    {
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'

        // To know about @Location
        compile 'com.sun.xml.bind:jaxb-core:2.2.11'
        runtime 'org.eclipse.persistence:org.eclipse.persistence.moxy:2.6.4'

        testCompile 'junit:junit:4.12'
    }
    // Wiring the generation of jaxb classes into standard compilation task:
    compileJava.dependsOn copyGenerated

    javadoc
    {
        // XJC generates Java8-unfriendly javadoc. Do not bother generating.
        excludes = ['**/generated/*.java']
    }
}

project(':wres-grid')
{
    dependencies
    {
        compile project(':wres-config')
        compile project(':wres-util')
        compile project(':wres-system')
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile('edu.ucar:cdm:5.0.0-alpha3')
        {
            // Because we use slf4j, not jcl:
            exclude group: 'commons-logging', module: 'commons-logging'
        }

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha4'

        // Because cdm uses apache commons logging, bridge to slf4j
        runtime 'org.slf4j:jcl-over-slf4j:1.8.0-beta2'

        testCompile 'junit:junit:4.12'
    }
}

project(':wres-messages')
{
    apply plugin: 'com.google.protobuf'

    def generatedDir = "${project.projectDir}/src"

    protobuf
    {
        // Automagically download and use the correct protobuf compiler
        protoc
        {
            artifact = 'com.google.protobuf:protoc:3.5.1-1'
        }
        // So all IDEs/editors can see generated files, use actual src dir:
        generatedFilesBaseDir = "${generatedDir}"
    }

    sourceSets
    {
        main
        {
            // Deviate from our typical source tree here to work around plugin's
            // inflexibility on paths plus IDEs handling generated code, see
            // https://github.com/google/protobuf-gradle-plugin/issues/190
            // The different srcDirs seems to work for eclipse, not IDEA.
            // But somehow a bunch of weird src tree shows up too.
            java
            {
                srcDirs = ['src/main/java']
            }

            // Tell protobuf plugin where to find .proto schema files:
            proto
            {
                srcDirs = ['nonsrc']
            }
        }
    }

    javadoc
    {
        // Protobuf does not appear to generate javadoc, so neither will we.
        excludes = ['**/generated/*.java']
    }

    dependencies
    {
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile 'com.google.protobuf:protobuf-java:3.5.1'

        testCompile 'junit:junit:4.12'
    }
}


project(':wres-worker')
{
    dependencies
    {
        compile project( ':wres-messages' )
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile 'com.rabbitmq:amqp-client:5.2.0'
        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha4'
    }

    apply plugin: 'application'

    mainClassName = 'wres.worker.Worker'
    applicationDefaultJvmArgs = ['-Xms32m','-Xmx32m',
                                 '-XX:+HeapDumpOnOutOfMemoryError']

    // Add logging configuration to the classpath when running via script
    startScripts
    {
        // Note: see lengthy note in main application below
        classpath += files('conf')
    }

    // Make available logging configuration to the classpath
    distributions
    {
        main
        {
            contents
            {
                from
                {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }
}

project(':wres-tasker')
{
    dependencies
    {
        compile project( ':wres-messages' )
        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile 'com.rabbitmq:amqp-client:5.2.0'
        compile 'javax.ws.rs:javax.ws.rs-api:2.1'
        compile 'javax.annotation:javax.annotation-api:1.3.2'

        // For Java 9+ compatibility:
        compile 'javax.xml.bind:jaxb-api:2.2.11'

        // Server library to run a web server:
        compile 'org.eclipse.jetty:jetty-server:9.4.12.v20180830'

        // Servlet container library to run a web application with:
        compile 'org.eclipse.jetty:jetty-webapp:9.4.12.v20180830'

        // Needed at compile-time to reference ServletContainer.class and
        // DefaultServlet.class
        compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.27'

        compile 'com.github.ben-manes.caffeine:caffeine:2.6.2'

        runtime 'ch.qos.logback:logback-classic:1.3.0-alpha4'
        runtime 'org.glassfish.jersey.core:jersey-server:2.27'
        runtime 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.27'

        // Needed to prevent "java.lang.IllegalStateException: InjectionManagerFactory not found."
        // Thanks, https://luohuahuang.org/2017/11/13/jersey-illegalstateexception-injectionmanagerfactory/
        runtime 'org.glassfish.jersey.inject:jersey-hk2:2.27'

        testCompile 'junit:junit:4.12'

        // To generate fake certificates and keys for testing
        testCompile 'org.bouncycastle:bcpkix-jdk15on:1.59'
        testCompile 'org.bouncycastle:bcprov-jdk15on:1.59'

        // To test against an embedded AMQP broker
        testCompile 'org.apache.qpid:qpid-broker-core:7.0.4'
        // Provides support for both 0-8 and 0-9-1
        testCompile 'org.apache.qpid:qpid-broker-plugins-amqp-0-8-protocol:7.0.4'
        testCompile 'org.apache.qpid:qpid-broker-plugins-memory-store:7.0.4'
    }

    apply plugin: 'application'

    mainClassName = 'wres.tasker.Tasker'
    applicationDefaultJvmArgs = ['-Xms160m','-Xmx160m',
                                 '-XX:+HeapDumpOnOutOfMemoryError']

    // Add static html files to the classpath when running via script
    startScripts
    {
        // Note: see lengthy note in main application below
        classpath += files('static')
        classpath += files('conf')
    }

    // Make available static html files to classpath, also logging configuration
    distributions
    {
        main
        {
            contents
            {
                from
                {
                    // Note: see lengthy note in main application below
                    ['dist']
                }
            }
        }
    }
}



/* Single report for test coverage, thanks to
https://discuss.gradle.org/t/merge-jacoco-coverage-reports-for-multiproject-setups/12100/5
 */
apply plugin: 'jacoco'        // code coverage tool
apply plugin: 'org.sonarqube' // static analysis tool

task(jacocoRootReport, type: JacocoReport, group: 'Verification', dependsOn: [subprojects.test, test])
{
    description = 'Generates an all-subproject aggregate code coverage report.'

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs,
                              sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output,
                             sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData,
                          jacocoTestReport.executionData)

    doFirst
    {
        executionData = files(executionData.findAll { it.exists() })
    }
}

/**
 * Task to compile markdown to html
 */
task(buildDocs, group: 'Documentation')
{
    description = 'Compiles markdown documentation into html'
    def srcDir = "${project.projectDir}/documentation/src" // seems odd that we have to do this
    def destDir = "${project.buildDir}/doc" // define these and use them later.

    inputs.dir(srcDir)
    outputs.dir(destDir)

    doFirst {
        // in case a source was removed, get rid of all html files.
        delete fileTree(dir: destDir).include('**/*.html').include('**/*.css')
    }

    doLast {
        def destFile = new File(destDir)
        destFile.mkdirs()

        def parser = org.commonmark.parser.Parser.builder().build()
        def renderer = org.commonmark.renderer.html.HtmlRenderer.builder().build()
        inputs.files.each {
            def basePath = Paths.get( it.path )
                                .getFileName()
                                .toString()

            if ( it.path.endsWith( ".md" ) )
            {
                // yuck, but I haven't found the better way yet
                def srcIdx =  it.path.indexOf( srcDir )
                def baseFileName = basePath.substring( 0, basePath.length() - 3 )

                // The meat - using commonmark to transform md to html
                def document = parser.parseReader( new FileReader( it ) )
                def innerOutput = renderer.render( document )
                def header = "<!DOCTYPE html>\r\n"
                header += "<html><head>\r\n"
                header += "    <link rel='stylesheet' type='text/css' href='style.css' />\r\n"
                header += "</head>\r\n<body>\r\n"
                def footer = "</body>\r\n</html>\r\n"
                def output = header + innerOutput + footer
                def outputPath = Paths.get( destDir, baseFileName + '.html' )
                Files.write( outputPath,
                             output.getBytes(),
                             StandardOpenOption.CREATE )
            }
            else
            {
                def outputPath = Paths.get( destDir, basePath )
                Files.copy( it.toPath(),
                            outputPath,
                            StandardCopyOption.REPLACE_EXISTING )
            }
        }
    }
}

/**
 * Kind of like xjc, there is an intermediate location in the build directory
 * so that gradle clean will remove these files.
 */
task(copyDocs, type: Copy, group: 'Documentation', dependsOn: buildDocs)
{
    description = 'Copies documentation source to distribution directory.'

    destinationDir = file("${project.projectDir}/dist/doc")

    doFirst {
        // in case a source file was removed, clean up old html files.
        delete fileTree(dir: destinationDir).include('**/*.html')
    }

    from buildDocs
    into destinationDir
}

apply plugin: 'application'

mainClassName = 'wres.Main'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

applicationDefaultJvmArgs = ['-Xms1628m', '-Xmx1628m',
                             '-XX:+HeapDumpOnOutOfMemoryError',
                             '-Djava.util.logging.config.class=wres.system.logging.JavaUtilLoggingRedirector']

// Get the commit and date from the TOP for this project, it is outermost.
def gitCommit = grgit.head()
def gitCommitId = gitCommit.id
def gitCommitDate = gitCommit.getDate()
def gitIsClean = grgit.status().isClean()

version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
if (!gitIsClean)
{
    version = version + '-dev'
}

apply plugin: 'nebula-aggregate-javadocs'

dependencies
{
    compile project(':wres-util')
    compile project(':wres-system')
    compile project(':wres-datamodel')
    compile project(':wres-io')
    compile project(':wres-metrics')
    compile project(':wres-config')
    compile project(':wres-vis')

    compile 'org.slf4j:slf4j-api:1.8.0-beta2'

    compile 'javax.ws.rs:javax.ws.rs-api:2.1'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'org.eclipse.jetty:jetty-server:9.4.12.v20180830'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.12.v20180830'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.27'

    compile 'com.github.ben-manes.caffeine:caffeine:2.6.2'

    runtime 'org.glassfish.jersey.core:jersey-server:2.27'
    runtime 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.27'
    runtime 'org.glassfish.jersey.inject:jersey-hk2:2.27'

    runtime 'ch.qos.logback:logback-classic:1.3.0-alpha4'


    testCompile group: 'commons-io', name: 'commons-io', version: '2.6'
    testCompile group: 'com.google.guava', name: 'guava-testlib', version: '21.0'
    testCompile group: 'com.google.guava', name: 'guava', version: '22.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'
}

sourceSets
{
    main
    {
        java
        {
            srcDirs = ['src', 'dist/lib/conf']
        }

        resources
        {
            srcDirs = ['nonsrc']
        }
    }

    test
    {
        java
        {
            srcDirs = ['test']
            exclude 'wres/systests/**'
        }

        resources
        {
            srcDirs = ['testNonsrc']
        }
   
    }
}

// Allows this:
// gradlew run -PappArgs="['saveForecast','/path/to/file']"
run
{
    if (project.hasProperty("appArgs"))
    {
        args Eval.me(appArgs)
    }
    // Allow profiling during run
    //jvmArgs = ['-agentlib:hprof=cpu=samples,interval=5,depth=25']
}

// Default location in gradle is 'src/dist' but we want conf files that are
// outside the jar to be from the 'dist' directory.
distributions
{
    main
    {
        contents
        {
            from
            {
                // Note: to have files land in the distributed zip files,
                // must put inside lib directory inside below directory.
                // Must also add the new directory to classpath under
                // startScripts below. For each module that adds to dist,
                // add here for now until we can make it grab all of em.
                ['dist', 'wres-vis/dist', 'wres-io/dist']
            }
        }
    }
}

// hook to get documentation into default zip/tar/etc.
processResources.dependsOn(copyDocs)

// Add configuration files to the classpath when running via script
startScripts
{
    // Note gradle in this context assumes lib directory precedes these
    // files. To add more outsidejar directories, they need to be
    // added to 'dist/lib' and then the directory name
    // can be added below.
    classpath += files('conf')
}

jar
{
    // toward reproducible builds:
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    // version number should be included
    manifest
    {
        attributes("Implementation-Title": "Water Resources Evaluation Service",
                   "Implementation-Version": version)
    }
}
