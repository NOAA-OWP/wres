plugins
{
    // for ability to use git ids for versioning jar files:
    id 'org.ajoberstar.grgit' version '5.0.0'
}

apply plugin: 'distribution'

// Each module does not need a version update unless code in it has changed.
def gitCommit = grgit.log(paths: ['scripts'], maxCommits:1).find()
def gitCommitId = gitCommit.id
def gitCommitDate = gitCommit.dateTime.withZoneSameInstant( ZoneId.of( "UTC" ) )

def gitIsClean = grgit.status().isClean()

version = gitCommitDate.format('yyyyMMdd') + '-' + gitCommitId[0..6]
if (!gitIsClean)
{
    version = version + '-dev'
}

distributions
{
    main
    {
        contents
        {
            from('.')
            {
                include 'cacerts/*.pem'
                include 'deprecated/pg_dump_wres.sh'
                include 'deprecated/pg_restore_wres.sh'
                include 'operational/wres_http_migrate_dbs_v2.sh'
                include 'operational/wres_http_cleandatabase.sh'
                exclude 'scenario*/output'
            }
        }
    }
}
